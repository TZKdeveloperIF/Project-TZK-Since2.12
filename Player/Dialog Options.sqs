; args: none

_idcMoney = IDC+0
_idcScore = IDC+1
_idcTowns = IDC+2
_idcIncome = IDC+3
_idcNumberedAI = IDC+4
_idcTransferGroup = IDC+5
_idcTransferAmount = IDC+6
_idcGroupMarkers = IDC+7
_idcReports = IDC+8
_idcIncomeRatio = IDC+9
_idcTransferCommand = IDC+10
_idcTransferCommandGroup = IDC+11
_idcCommandAI = IDC+12
_idcAIRespawn = IDC+13
_idcUpgradeStart = IDC+14
_idcUpgradeList = IDC+15
_idcWorkerBehaviour = IDC+16
_idcDestruction = IDC + 17
_idcIncomeRatioPlayer = IDC+18
_idcGiveMoneyToAI = IDC + 19
_idcTakeMoney = IDC + 20
_idcCommandTemporary = IDC + 21
_idcSpySat = IDC + 22
_idcGiveMoneyToALL = IDC + 23

; _idc defined above must consist with definition in description.ext.


_unit = player
_dlg = createDialog "OptionsDialog"

ctrlShow [IDC_DEFAULT, false]
ctrlShow [_idcNumberedAI, false]

btnLeaderboard = false
btnDestruction = false
btnGrant = false 
btnAsk = false
btnTake = false
btnGive = false
btnGiveAIs = false
btnGiveAll = false
btnUpgrade = false
btnBuyUnits = false
btnShowStats = false
btnUnitCam = false
btnSatCam = false
btnCustomSoldiers = false
btnCommandAI = false
btnCommandPlayer = false
btnTransfer = false
btnNumberedAI = false
btnAICheck = false
btnCommandTemporary = false
btnSpySat = false


_report = report

_groups = groupMatrix select siPlayer
_groupsName = groupNameMatrix select siPlayer
_groupsMoney = groupMoneyMatrix select siPlayer
_groupsAI = groupAiMatrix select siPlayer
_groupCommander = (groupCommander select siPlayer)

? (siPlayer == si0): _incomeRatio = pvIncomeRatio0; _incomeRatioPlayer = pvIncomeRatioPlayer0
? (siPlayer == si1): _incomeRatio = pvIncomeRatio1; _incomeRatioPlayer = pvIncomeRatioPlayer1

_res = [_idcTransferAmount, _idcIncomeRatio, _idcIncomeRatioPlayer, _idcGroupMarkers, _idcReports, _idcTransferCommandGroup, _idcWorkerBehaviour, _idcAIRespawn] call funcUpdateDialogOptionsInit
_currentAiRespawn = _res select 0; _respawnObjects = _res select 1

#Update

? !(dialog): Exit
? !(alive _unit): Exit

_money = _groupsMoney select giPlayer
_groupCommander = (groupCommander select siPlayer)
; DISABLE stuff for NON-commanders
_isCommander = groupPlayer == _groupCommander
ctrlEnable [_idcAIRespawn, _isCommander]; ctrlEnable [_idcIncomeRatio, _isCommander]; ctrlEnable [_idcIncomeRatioPlayer, _isCommander]; ctrlShow [_idcUpgradeStart, _isCommander]; ctrlEnable [_idcTransferCommandGroup, _isCommander]; ctrlShow [_idcTransferCommand, _isCommander]; ctrlEnable [_idcWorkerBehaviour, _isCommander]; ctrlShow [_idcDestruction, _isCommander]; ctrlShow [_idcGiveMoneyToAI, _isCommander]; ctrlShow [_idcGiveMoneyToALL, _isCommander]; ctrlShow [_idcTakeMoney, _isCommander]
? !_isCommander : _gis = ([siPlayer] call funcGetAIGroupsIndex); _Superior = _gis call funcCheckWhetherSuperior; if (_Superior && lbCurSel _idcTransferGroup != -1) then {ctrlShow [_idcTakeMoney, ( ( (["Commander"] + callsigns) select ( ( (aiSetting select siPlayer) select lbCurSel _idcTransferGroup ) select aisSuperior ) ) == _groupsName select giPlayer) || ( (_groups select (lbCurSel _idcTransferGroup)) == _groupCommander )]}

ctrlShow [_idcNumberedAI, bNumberedAI]
ctrlShow [_idcSpySat, bSpySat]

; AI RESPAWN POS
? (_currentAiRespawn != (lbCurSel _idcAIRespawn)): _currentAiRespawn = (lbCurSel _idcAIRespawn); call format["pvRespawnObjectAi%1 = (_respawnObjects select _currentAiRespawn); PublicVariable ""pvRespawnObjectAi%1""", siPlayer]

[_idcMoney, _idcScore, _idcIncomeRatio, _idcIncomeRatioPlayer, _idcTowns, _idcIncome, _idcTransferGroup, _idcGroupMarkers, _idcWorkerBehaviour, _idcUpgradeList ] call funcUpdateDialogOptionsUpdate


; REPORTS
report = lbCurSel _idcReports
? _report != report : goto "SaveReport"

;? btnGrant : btnGrant=false; goto "GrantMoney"
;? btnAsk : btnAsk=false; goto "AskMoney"
? btnTake : btnTake=false; goto "TakeMoney"
? btnGive : btnGive=false; goto "GiveMoney"
? btnGiveAIs : btnGiveAIs=false; goto "GiveMoneyToAI"
? btnGiveAll : btnGiveAll=false; goto "GiveMoneyToAll"
? btnBuyUnits : btnBuyUnits=false; goto "BuyUnits"
? btnShowStats : btnShowStats=false; goto "ShowStats"
? btnUnitCam : btnUnitCam=false; goto "UnitCam"
? btnSatCam : btnSatCam=false; goto "SatCam"
? btnUpgrade : btnUpgrade=false; goto "Upgrade"
? btnLeaderboard : btnLeaderboard=false; goto "Leaderboard"
? btnCommandAI : btnCommandAI=false; goto "CommandAI"
? btnCommandPlayer : btnCommandPlayer=false; goto "CommandPlayer"
? btnTransfer : btnTransfer=false; goto "TransferCommand"
? btnCustomSoldiers : btnCustomSoldiers=false; goto "CustomSoldiers"
? btnNumberedAI : btnNumberedAI=false; goto "SaveNumberedAI"
? btnDestruction : btnDestruction=false; goto "DestructBuildings"
? btnAICheck : btnAICheck = false; goto "CheckAI"
? btnCommandTemporary : btnCommandTemporary = false; goto "CommandTemporary"
? btnSpySat : btnSpySat = false; goto "SpySat"


~0.2
goto "Update"

;#GrantMoney
;	_index = lbCurSel _idcTransferGroup
;	? (_index == -1): hint "No Group Selected"; goto "Update"
;	_group = _groups select _index
;	? (groupPlayer == _group): hint "You are trying to grant money to yourself"; goto "Update"
;	? (_group in _groupsAI): hint "You can not grant Money to an AI"; goto "Update"
;      call format [{grant_%1_%2_%3 = !grant_%1_%2_%3; publicvariable "grant_%1_%2_%3"; },siPlayer,giPlayer,_index]
;	goto "Update"

;#AskMoney
;	_index = lbCurSel _idcTransferGroup
;	? (_index == -1): hint "No Group Selected"; goto "Update"
;	_group = _groups select _index
;	? (groupPlayer == _group): hint "You are trying to give money to yourself"; goto "Update"
;	? (_group in _groupsAI && _group != _groupCommander): hint "You can only ask money from an AI commander"; goto "Update"
;	_amount = lbValue [_idcTransferAmount, lbCurSel _idcTransferAmount]
;      _granted = false;
;      call format [{_granted = grant_%1_%2_%3},siPlayer,_index,giPlayer]; _grantedx = true;
;      ? ((_amount <= 500) and (((groupMoneyMatrix select siPlayer) select giPlayer) < 500) and (((groupMatrix select siPlayer) select _index) == _groupCommander)): _granted = true;
;	? (_granted): [siPlayer, _index, giPlayer, _amount] exec "Player\SendGiveMoney.sqs"
;      ? (! _granted): hint "Permission Denied!"; _grantedx = false;
;	goto "Update"

#GiveMoney
	_index = lbCurSel _idcTransferGroup
	? (_index == -1): hint "No Group Selected"; goto "Update"
	_group = _groups select _index
	? (groupPlayer == _group): hint "You are trying to give money to yourself"; goto "Update"
	; ? (_group in _groupsAI && _group != _groupCommander): hint "You can only give money to an AI commander"; goto "Update"
	_amount = lbValue [_idcTransferAmount, lbCurSel _idcTransferAmount]
	? _amount == 114514 : _amount = ( _money % 100 )
	? _amount == 50000 : _amount = _money
	[siPlayer, giPlayer, _index, _amount] exec "Player\SendGiveMoney.sqs"
	goto "Update"
	
#GiveMoneyToAI
	_amount = lbValue [_idcTransferAmount, lbCurSel _idcTransferAmount]
	? _amount == 114514 : _amount = ( _money % 100 )
	? _amount == 50000 : _amount = _money / count(_groupsAI) - ( ( _money / count(_groupsAI) ) % 1 )
	[siPlayer, giPlayer, giPlayer, _amount] exec "Player\SendGiveMoney.sqs"
	goto "Update"

#GiveMoneyToAll
	_amount = lbValue [_idcTransferAmount, lbCurSel _idcTransferAmount]
	? _amount == 114514 : _amount = ( _money % 100 )
	? _amount == 50000 : _amount = _money / count(_groups - [_groupCommander]) - ( ( _money / count(_groups - [_groupCommander]) ) % 1 )
	_amount = - _amount
	[siPlayer, giPlayer, giPlayer, _amount] exec "Player\SendGiveMoney.sqs"
	goto "Update"


#TakeMoney
	_index = lbCurSel _idcTransferGroup
	? (_index == -1): hint "No Group Selected"; goto "Update"
	_group = _groups select _index
	? (groupPlayer == _group): hint "You are trying to take money from yourself"; goto "Update"
	? (groupPlayer != _groupCommander) : if ( ( ((["Commander"] + callsigns) select ( ((aiSetting select siPlayer) select _index) select aisSuperior )) != _groupsName select giPlayer) && _group != _groupCommander ) then { hint "You can only take money from Commander or Your Inferior."; goto "Update" }
	_amount = lbValue [_idcTransferAmount, lbCurSel _idcTransferAmount]
	? _amount == 114514 : _amount = ( (_groupsMoney select _index) % 100 )
	? _amount == 50000 : _amount = (_groupsMoney select _index)
	[siPlayer, giPlayer, _index, -_amount] exec "Player\SendGiveMoney.sqs"
	goto "Update"
	
;#DisbandUnit
;	_index = lbCurSel IDC_LB_UNITS
;	? (_index == -1): hint "No Unit Selected"; goto "Update"
;	_ai = _units select _index
;	? !(isNull _ai) && (alive _ai) && (local _ai): _ai setDamage 1
;	~1
;	? !(alive _ai): deleteVehicle _ai
;	goto "Update"

#Upgrade
	_index = lbCurSel _idcUpgradeList
	? (_index == -1): hint "No Upgrade Selected"; goto "Update"

	_controlCenters = [siPlayer, stComm] call funcGetWorkingStructures
	? ((count _controlCenters) == 0): hint "No Working Comm Center"; goto "Update"

	_cost = (upgDefs select _index) select 1
	? (_money < _cost): hint "Not Enough Cash"; goto "Update"

	[_index] exec "Player\SendUpgrade.sqs"
	goto "Update"

#BuyUnits
	_controlCenters = [siPlayer, stComm] call funcGetWorkingStructures
	? ((count _controlCenters) == 0): Hint "No Working Comm Center"; goto "Update"
	
	closeDialog 0
	~0.1
	[objNull, 0] exec "Player\Dialog BuyUnits.sqs"
	Exit

#Leaderboard
	closeDialog 0
	~0.1
	[] exec "Player\Dialog Leaderboard.sqs"
	Exit

#ShowStats
	closeDialog 0
	~0.1
	[siPlayer] exec "Player\Dialog Stats.sqs"
	Exit

#UnitCam
	_controlCenters = [siPlayer, stSatRec] call funcGetWorkingStructures
	? ((count _controlCenters) == 0): Hint "No Working Camera Receiver"; goto "Update"

	; ? (2 != (upgMatrix select siPlayer) select upgUnitCam): Hint "Not available yet\n(not upgraded)"; goto "Update"

	closeDialog 0
	~0.1
	[unitcamUnit] exec "Player\Dialog UnitCam.sqs"
	Exit

#SatCam
	; _controlCenters = [siPlayer, stComm] call funcGetWorkingStructures
	; ? ((count _controlCenters) == 0): Hint "No Working Comm Center"; goto "Update"
	_satrec = [siPlayer, stSatRec] call funcGetWorkingStructures
	? ((count _satrec) == 0): Hint "No Working Camera Receiver"; goto "Update"

	? (2 != (upgMatrix select siPlayer) select upgSatCam): Hint "Not available yet\n(not upgraded)"; goto "Update"

	closeDialog 0
	~0.1
	[] exec "Player\Dialog SatCam.sqs"
	Exit

#CustomSoldiers
	closeDialog 0
	~0.1
	[player, player, true, 0] exec "Player\Dialog Equipment.sqs"
	Exit

#CommandAI
	closeDialog 0
	~0.1
	[] exec "Player\Dialog AIGroupOrders.sqs"
	Exit
	
#CommandPlayer
	closeDialog 0
	~0.1
	[] exec "Player\Dialog PlayerGroupOrders.sqs"
	Exit
	
#TransferCommand
	_idc = _idcTransferCommandGroup
	_gi = lbValue [_idc, lbCurSel _idc]
  _group = (groupMatrix select siPlayer) select _gi
	? (_group == _groupCommander): goto "Update"
	? (_group in (groupAiMatrix select siPlayer)): hint "You cant transfer command to an AI group leader"; goto "Update"
	pvCO = _gi + GroupsNum*siPlayer; PublicVariable "pvCO"
	goto "Update"

#SaveReport
  _report = report
  unitSetting = "SoldierWB" camCreate [10,10,10]
  removeAllWeapons unitSetting
  unitSetting addMagazine "M16"
  _i = 0
  while "_i < _report" do { unitSetting addMagazine "M16"; _i=_i+1 }
  _entry = format["%1_EnemyReportLevel", localize "STR_CRCTI_VER"]
  unitSetting saveStatus _entry
  deleteVehicle unitSetting
  goto "Update"

#SaveNumberedAI
  bNumberedAI = !bNumberedAI
  unitSetting = "SoldierWB" camCreate [10,10,10]
  removeAllWeapons unitSetting
  unitSetting addMagazine "M16"
  ? bNumberedAI : unitSetting addMagazine "M16"
  _entry = format["%1_NumberedAI", localize "STR_CRCTI_VER"]
  unitSetting saveStatus _entry
  deleteVehicle unitSetting
  goto "Update"

#DestructBuildings
	closeDialog 0
	~0.1
	[] exec "Player\Dialog Destruction.sqs"
	Exit

#CheckAI
	_index = lbCurSel _idcTransferGroup
	? (_index == -1): hint "No Group Selected"; goto "Update"
	_group = _groups select _index
	? (groupPlayer == _group): hint "Well I think you're not an AI since you're so smart that you are tend to check whether you are an AI."; goto "Update"
	? _group in _groupsAI : hint "This group is AI."; goto "Update"
	_index exec "Player\CheckAI.sqs"; hint "Checking whether the group is AI. Please wait for the result."
	goto "Update"

#CommandTemporary
	closeDialog 0
	~0.1
	[] exec "Player\Dialog AITemporaryOrders.sqs"
	Exit


#SpySat
	_satrec = [siPlayer, stSatRec] call funcGetWorkingStructures
	? ((count _satrec) == 0): Hint "No Working Camera Receiver"; goto "Update"

	? (2 != (upgMatrix select siPlayer) select upgSatCam): Hint "Need upgrade Satellite Camera first."; goto "Update"
	? (2 != (upgMatrix select siPlayer) select upgSpySat): Hint "Need upgrade Spy Satellite first."; goto "Update"

	bSpySat = !bSpySat; if (call format ["count SpySatPos%1 == 0", siPlayer]) then {call format ["SpySatPos%1 = getPos player", siPlayer]}
	goto "Update"