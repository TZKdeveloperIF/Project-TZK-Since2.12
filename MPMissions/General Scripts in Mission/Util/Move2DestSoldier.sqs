; args: [output status (write only), input status (read only), unit, dest pos, time abort]

; this SQS script loops to make soldier moving to destination position
; on script finished/aborted, the output status will be set to "false"
; if input status is false, current order should be interrupted and halt
; do in-script exit check ONLY for those conditions whose parameters are local
; no other in-script exit check
; it is possible but unnecessary to use only one status variable

_unit = _this select 2, _destPos = _this select 3, _timeAbort = _this select 4

~ 0.1

_threshold = 2
_distH = [_unit, _destPos] call funcDistH
_try = 0

_unit doMove _destPos; _unit setUnitPos "UP"

# MoveToSupport
	; don't send "move" order if too close, otherwise AI will "stop"
	? _distH > 15: _unit doMove _destPos; _unit setUnitPos "UP"

	_delay = (if (_distH < 300) then {_distH / 10} else {30})
	? _delay < 2: _delay = 2
	~ _delay
	? not (_this select 1): goto "EOF"
	? _time > _timeAbort && _timeAbort > 0: goto "EOF"

	_distH = [_unit, _destPos] call funcDistH
	? _distH < 5: _try = _try + 1
	? _try > 3: goto "EOF"
	? _distH > _threshold: goto "MoveToSupport"
# EOF
	_this set [0, false]