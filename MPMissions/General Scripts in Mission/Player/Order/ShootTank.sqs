; args: [unit, marker, target, vehicle type, dispersion, (bigger angle)]
? localize {TZK_ART_406} != "true": hint "TZK_Art_4_0_6.pbo isn't detected. Art module inactivated.", exit

_unit = _this select 0
_si = siPlayer; _uid = [_unit, _si, giPlayer] call funcCalcUID
? _uid > 12: hint "Fail to get UID, order not set. Try again please."; exit
_id = (playerOrderID select _uid) + 1; [_uid, _id] exec "\TZK_Scripts_4_0_4\Player\Order\New.sqs"
? !(_unit in PlayerShootingUnits): PlayerShootingUnits set [_uid, _unit]

_marker = _this select 1; _target = _this select 2
_dispersion = _this select 4; _type = _this select 3
_biggerAngle = (if (count _this > 5) then {_this select 5} else {0})
_biggerAngle = (if (_biggerAngle > 0) then {true} else {false})

_this = 0
_longRangeCoef = 1.5
; big angle will be invalid when ordering tanks.
? _type in (typesLightTank + typesHeavyTank): _biggerAngle = false; _longRangeCoef = 2

_quitCond = {call orderCheck || not alive _unit || _vehicle != vehicle _unit}
_msg = ""
_isServer = false; _isVehicle = true; _isStruct = false;


_vehicle = vehicle _unit; _typeName = typeOf _vehicle; _script = "Common\EH\Fired_Art_406.sqs"
? _biggerAngle: _heliH = typeOf _target camCreate [0,0,0]
# CheckDistance
	_posOrdered = getPos _vehicle; _distLimit = viewDistance; _hint = "Target is outside viewDistance. Order aborted."
	? upgMatrix select _si select upgLongWeaponRange == 2: _distLimit = _longRangeCoef * viewDistance; _hint = format ["Target is outside %1x viewDistance. Order aborted.", _longRangeCoef]
	? _unit distance _target > _distLimit: _msg = _hint; goto "EOF"
	? _unit distance _target < _distLimit && _unit distance _target > viewDistance: _unit groupChat "Target is far. Probably missing."



# PrepareToMove
; For player controlled tanks, the order will directly use its current position as shooting position. This position is restored only for return after having rearmed.

? ([getPos _unit, _posOrdered] call funcDistH) < 30: _unit doMove _posOrdered, goto "PrePareToAttack"
_unit groupChat "Moving to attacking position."; _timeStart = time; _timeHint = _timeStart + 5*60;
_hint = ["I've been running for %1 minutes. Distance %2 meters now.", (_timeHint -_timeStart)/60, (_unit distance _target) - (_unit distance _target)%1]
_vehicle exec "Art\Undeploy.sqs"

# CheckPosReached
	_unit doMove _posOrdered
	~5
	? call _quitCond: _msg = ""; goto "EOF"
	? ([getPos _unit, _posOrdered] call funcDistH) > 30: goto "CheckPosReached"
	? time > _timeHint: hint format _hint; _timeHint = _timeHint + 5*60
# PrePareToAttack
	_delay = 0
	_ret = call preprocessFile "Art\AimAndCalcDelay.sqf"
	? not _ret: goto "EOF"
	~ _delay

	_weapon = ""
	_ret = call preprocessFile "Art\PrepareWeapon.sqf"
	? not _ret: goto "EOF"
# PreCalculate
	? _unit distance _target > _distLimit: goto "CheckDistance"

	_ammoArray = _vehicle ammoArray _weapon; _equippedMag = ""; _ammoOld = 0;
	_ret = call preprocessFile "Art\PreAnalysis.sqf"
	? not _ret: goto "EOF"

	_posV = getPosASL _vehicle; _posT = getPosASL _target; _posT set [2, _posT call funcHASL]
	_speed = call preprocessFile "Art\GetInitSpeed.sqf"
	_angle = 0; _angleDelta = 0; _elev = 0; _thetaDelta = 0
	_ret = call preprocessFile "Art\PreCalculate.sqf"
	? not _ret: goto "EOF"

	; outer variables for attack loop
	_delay = 0; _reloadMagazine = false
# AttackCheck
	; 当载具的位置发生了变化 (变化10米以上), 或目标的位置发生了变化, 或弹夹发生了切换, 需要重新回到试射环节.
	? 10 < [_vehicle, _posV] call funcDistH || 10 < [_target, _posT] call funcDistH: goto "PreCalculate"
	_currentMag = _equippedMag
	? _unit distance _target > _distLimit: goto "CheckDistance"
	? _biggerAngle: _posH = [_posT, _posV] call funcVectorSub, _posH set [2, 0], _length = _posH call funcVectorLength, _posH = [_posH, 300/_length] call funcVectorScale, _posH set [2, 300* 1.732], _posH = [_posH, _posV] call funcVectorAdd, _heliH setPosASL _posH

	? _biggerAngle: _vehicle reveal _heliH
	_vehicle reveal _target
	~0.1
	_vehicle doTarget (if _biggerAngle then {_heliH} else {_target})

	~0.1
	? call _quitCond: _msg = ""; goto "EOF"
	? _vehicle knowsAbout _target < 0.01 && _vehicle distance _target > viewDistance: _msg = "Can't locate the target. Order Aborted. Help me spot it again please."; goto "EOF"

	? 0 == _ammoOld: _magazines = _vehicle call funcGetNotEmptyMags; _validMags = [_weapon] call funcWeaponValidMags; if ({_x in _validMags} count _magazines == 0) then {goto "CheckRearm"};

	; _ammoArray is initialized before the loop and updated after having shot while before next loop delay
	? count _ammoArray == 0: _msg = "Order Aborted. Help me ""Reload"" the magazine (via 6-action list) before ask me shoot again."; goto "EOF"
	_currentMag = _ammoArray select 0
	? _currentMag != _equippedMag: goto "PreCalculate"

	; no way in 2.01 to know when will a mag is loaded. Force shoot and wait until shooting actually happen using "_ammoOld"
	~ _delay
	? call _quitCond: _msg = ""; goto "EOF"
	; check ammoarray again since delayed
	_ammoArray = _vehicle ammoArray _weapon
	? count _ammoArray == 0: _msg = "Order Aborted. Help me ""Reload"" the magazine (via 6-action list) before ask me shoot again."; goto "EOF"
	_currentMag = _ammoArray select 0
	? _currentMag != _equippedMag: goto "PreCalculate"

	? not local gunner _vehicle: _delay = 5; goto "AttackCheck"
	? "BLUE" == combatMode _vehicle || "GREEN" == combatMode _vehicle: _delay = 5; goto "AttackCheck"
	? 1 == _ammoOld: _reloadMagazine = true

	_ehRev = _vehicle addEventHandler ["Fired",""], _vehicle removeEventHandler ["Fired", _ehRev]
	_vehicle addEventHandler ["Fired", format [{[_this, %1, %2, %3, %4] exec "%5"}, _speed, _angle - _angleDelta + _angleDelta*(random 2), _elev - _thetaDelta + _thetaDelta*(random 2), _ehRev, _script] ]
	_vehicle fire _weapon

	# w8UntilShooted
		_counter = 0
		~ 1
		_ammoArray = _vehicle ammoArray _weapon
		? 0 == count _ammoArray: _msg = "Order Aborted. Help me ""Reload"" the magazine (via 6-action list) before ask me shoot again."; goto "EOF"
		? call _quitCond: _msg = ""; goto "EOF"
		_counter = _counter + 1
		? _counter >= 30: _unit groupChat "Verify whether my main weapon is loaded, sir. If weapon is ready then reorder me and report a bug to script designer.", _counter = 0
		? _ammoArray select 1 == _ammoOld && _ammoOld != 1: goto "w8UntilShooted"

	_ammoOld = _ammoArray select 1
	_delay = (call format ["%1", (_ammoArray select 0) GetWeaponParam "reloadTime"]) * 1.1 + 0.5
	? _reloadMagazine: _delay = (call format ["%1", _weapon GetWeaponParam "magazineReloadTime"]) + _delay; _reloadMagazine = false
	_delay = _delay - 1;

	; AttackDone
	~ 0.1
	if _biggerAngle then {_vehicle doWatch _posH} else {_vehicle doWatch _posT}
	goto "AttackCheck"
# CheckRearm
	_vs = [getPos _unit, 1500, (typesSupport select si0) + (typesSupport select si1) + [(typesMHQ select _si)], []] call funcGetNearbyVehicles
	? count _vs == 0: _msg = "No support vehicle within 1500 meters. Magazines exhausted. Order aborted."; goto "EOF"
	_distMin = 1500; _sup = objNull
	{ _dist = _unit distance (_x select 0); if (_dist < _distMin) then { _sup = (_x select 0); _distMin = _dist } } forEach _vs
	? isNull _sup: _msg = "No support vehicle near. Magazines exhausted. Order aborted."; goto "EOF"
	_unit groupChat "Moving to support vehicle for more magazines."

	_timeAbort = time + 15*60
	_vehicle exec "Art\Undeploy.sqs"
	# MoveToSupport
		_posMove = [[0,25], (getDir _sup)] call funcVectorRot2D
		_posMove = [getPos _sup, _posMove] call funcVectorSub
		_unit doMove _posMove
		~5
		? call _quitCond: _msg = ""; goto "EOF"
		? !(alive _sup): goto "CheckRearm"
		? time > _timeAbort: _msg = "Cost too much time. Unable to reach support vehicle. Order aborted."; goto "EOF"
		? (_unit distance _sup) > 30: goto "MoveToSupport"
	_speed = -1; _mag = "";
	_magazines = magazines _vehicle; _validMags = [_weapon] call funcWeaponValidMags; {if (_x in _validMags) then {_mag = _x}} forEach _magazines
	? _mag != "": _speed = substr [_mag, sizeofstr(_mag) - sizeofstr("xxxx_xj400"), sizeofstr(_mag) - sizeofstr("_xj400")]; if !(substr [_speed, 0, 1] in ["0","1","2","3"]) then {_speed = -1} else {_speed = call _speed};
	_delay = [0,3] select (_speed > 0)
	_unit groupChat "Rearming at support vehicle. If I'm far away from you, probably I can't receive supplemented magazines. In this case, please use Unit Camera and look at me."; [_unit, _sup] exec "Player\Rearm.sqs"; doStop _unit
	# WaitRearmComplete
		~5
		? call _quitCond: _msg = ""; goto "EOF"
		? !someAmmo _vehicle: goto "WaitRearmComplete"
	~ _delay + 1
	? _speed <= 0: goto "PrepareToMove"
	_delay = 1; [_vehicle, _speed] exec localize {TZK_FUNC_SWITCH_MAG_PRESET}; _unit groupChat "Switching magazine. Again, if I'm far away, use Unit Camera and look at me."
	# WaitSwitchComplete
		~ _delay
		? call _quitCond: _msg = ""; goto "EOF"
		_delay = 5
		? !someAmmo _vehicle: goto "WaitSwitchComplete"
	goto "PrepareToMove"

# EOF
	PlayerShootingUnits set [_uid, objNull]
	? _biggerAngle: deleteVehicle _heliH
	? _msg != "": _unit groupChat _msg
	exit