; args: none
@ !mutexDlg
mutexDlg = true; dlgID = dlgID + 1; _dlgID = dlgID
_dlg = createDialog "CommandAIDialog"
mutexDlg = false

? !bool_TZK_CHN_inited && bool_TZK_CHN_Lang: call loadFile localize {TZK_PLAYER_DIALOG_CHNLANG}
_bTzkChn = bool_TZK_CHN_Lang; _bSettingDispInited = false

_si = siPlayer
_gi = -1

btnSendOrder = false; btnTemporaryOrder = false

_idcCurrent = IDC+0
_idcSettingLabel = IDC + 100
_idcSettingCombo = IDC + 200
_idcSettingImage = IDC + 300
_idcOrderLabel = IDC+1
_idcSendOrder = IDC+2
_idcOrder = IDC+3
_idcPar0Label = IDC+4
_idcPar0 = IDC+5
_idcPar1Label = IDC+6
_idcPar1 = IDC+7
_idcPar2Label = IDC+8
_idcPar2 = IDC+9

_cSettings = 26
_lastSetting = []
{ _lastSetting set [count _lastSetting, -1] } foreach aiSettingDefs

_groupNames = groupNameMatrix select _si

_selGroup = -1
_selOrder = -1

_idcParamLabels = [_idcPar0Label, _idcPar1Label, _idcPar2Label]
_idcParams = [_idcPar0, _idcPar1, _idcPar2]
{ ctrlShow [_x, false] } foreach _idcParamLabels, { ctrlShow [_x, false] } foreach _idcParams

call loadFile localize {TZK_DIALOG_AI_GRP_ORDER_INIT}

; functions
_update4OneGroup = preprocessFile "Player\Dialog\AiGrpOrder_Update4OneGroup.sqf"
_updateCtrls = preprocessFile "Player\Dialog\AIGrpOrder_UpdateControls.sqf"
_refreshOrder = preprocessFile "Player\Dialog\AiGrpOrder_RefreshOrder.sqf"
_funcLeadBy = preprocessFile "Util\GrpLeadBy.sqf";
; lazy defined process
_bInitSelNewGroup = false
_bInitSendSetting = false

# Update
ctrlShow [IDC_DialogBG_CR, !bool_TZK_DarkSkin]; ctrlShow [IDC_DialogBG_Light, bool_TZK_DarkSkin]
	? call dlgUpdated: exit
	? !(alive player): closeDialog 0; Exit

	lbClear _idcCurrent
	_gis = ([_si] call funcGetAIGroupsIndex)
	{_order = (orderMatrix select _si) select _x; _text = _order call funcGetOrderDesc; _id = lbAdd [_idcCurrent, format["%1, %2", _groupNames select _x, _text]]; lbSetValue [_idcCurrent, _id, _x]} forEach _gis
	? count _gis > 0 : _id = lbAdd [_idcCurrent, "ALL"]; lbSetValue [_idcCurrent, _id, -1]
	
	~ 0.2

	? _selGroup != lbCurSel _idcCurrent: goto "NewGroupSelected"
	? _selOrder != lbCurSel _idcOrder : _selOrder = lbCurSel _idcOrder; goto "NewOrderSelected"

	_set = -1; _i = 0; _c = count aiSettingDefs
	while {_i<_c && _set == -1} do {if !(lbCurSel (_idcSettingCombo + _i) in [_lastSetting select _i, -1]) then {_set = _i}; _i = _i + 1}
	? _set != -1 : goto "SendSetting"

	? btnSendOrder: btnSendOrder = false; goto "SendOrder"
	? btnTemporaryOrder: btnTemporaryOrder = false; goto "TemporaryOrder"

	; ~0.2
	goto "Update"

# NewGroupSelected
	? not _bInitSelNewGroup: _bInitSelNewGroup = true; _onSelNewGrp = preprocessFile "Player\Dialog\AIGrpOrder_OnNewGroupSelected.sqf"
	call _onSelNewGrp
	goto "Update"

# NewOrderSelected
	? _selOrder == -1 : goto "Update"
	true call _refreshOrder

	goto "Update"

# SendSetting
	? not _bInitSendSetting: _bInitSendSetting = true; _sendSetting = preprocessFile "Player\Dialog\AiGrpOrder_SendSetting.sqf"
	call _sendSetting
	goto "Update"

# SendOrder
  ? _selGroup == -1 : hint "No group selected"; goto "Update"
  ? _selOrder == -1 : hint "No order selected"; goto "Update"

	_params = [0,0,0]
	_paramsSet = true
	_index = 0
	{ if ( -1 == lbCurSel (_idcParams select _index)) then { _paramsSet = false }; _index = _index + 1 } foreach ((orderDefs select _selOrder) select 1)
	? !(_paramsSet): hint "Paramaters not set"; goto "Update"
	
	_index = 0
	{ _params set [_index, lbCurSel (_idcParams select _index)]; _index = _index + 1 } foreach ((orderDefs select _selOrder) select 1)

	? _gi == -1: [_si, giPlayer + GroupsNum, _selOrder, _params] call preprocessFile localize {TZK_MSG_COMMON_SEND_AI_GRP_ORDER}
	? _gi != -1: [_si, _gi, _selOrder, _params] call preprocessFile localize {TZK_MSG_COMMON_SEND_AI_GRP_ORDER}
	goto "Update"
	
# TemporaryOrder
	closeDialog 0
	; ~0.1
	0 exec localize {TZK_DIALOG_AI_CONCURRENT}
	Exit