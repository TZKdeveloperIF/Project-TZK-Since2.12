; args: pos
_pos = _this
_setTargetScript = "Player\Art\SetTarget.sqs"
@ !mutexDlg
mutexDlg = true; dlgID = dlgID + 1; _dlgID = dlgID
_dlg = createDialog "TzkWaypointsDialog"
mutexDlg = false

@ not mutexRtsMouse
	mutexRtsMouse = true
	_id = rtsMouseId
	mutexRtsMouse = false
[true, _id + 1, _pos] exec "Player\Rts\SetRtsMouse.sqs";

_idcAction0Label = IDC+0
_idcAction0 = IDC+1
_idcAction1Label = IDC+2
_idcAction1 = IDC+3
_idcAction2Label = IDC+4
_idcAction2 = IDC+5
_idcAction3Label = IDC+6
_idcAction3 = IDC+7
_idcAction4Label = IDC+8
_idcAction4 = IDC+9

_idcListHeader = IDC+10
_idcList = IDC+11

btnAction0 = false
btnAction1 = false

btnValue02 = -1

_idcBtn_0_0 = IDC + 16 * 2; _idcBtnBg_0_0 = IDC + 16 * 1;
{ctrlSetText [_x, "co"]} forEach [_idcBtn_0_0]
{ctrlSetText [_x, "wp"]} forEach [_idcBtn_0_0 + 1]
{ctrlSetText [_x, "Player Art"]} forEach [_idcBtn_0_0 + 2]
{ctrlSetText [_x, "Server Art"]} forEach [_idcBtn_0_0 + 3]
{_i = _x; {ctrlShow [_x + _i, false]} forEach [_idcBtnBg_0_0]} forEach [0,1,2,3,4]
_highlight = "CO", btnValue02 = 0

_co = isCommander; _Superior = bIsAiSuperior
? not _co && not _Superior: _highlight = "WP", btnValue02 = 1

_COcount = (countWPCO/2) - (countWPCO/2)%1

{ ctrlShow [_x, false] } forEach [_idcAction0Label, _idcAction1Label, _idcAction2Label, _idcAction3Label, _idcAction4Label]
{ ctrlShow [_x, _co] } forEach [_idcAction3, _idcAction4]

ctrlSetText [IDC_TITLE, "Waypoints"]
ctrlSetText [_idcAction0, "SetBtn"]
ctrlSetText [_idcAction1, "Clear"]
ctrlSetText [_idcAction2, "Clear All Player WPs"]
ctrlSetText [_idcAction3, "Clear All CO WPs"]
ctrlSetText [_idcAction4, "Order AI groups"]

_refreshList = preprocessFile "Player\Dialog\Wp\UpdateList.sqf"

ctrlSetText [_idcListHeader, "Player Waypoints"]
? _co || _Superior: ctrlSetText [_idcListHeader, "Commander/Player Waypoints"]

bNewCoSet = false; call _refreshList

# Update
	ctrlShow [IDC_DialogBG_CR, !bool_TZK_DarkSkin]; ctrlShow [IDC_DialogBG_Light, bool_TZK_DarkSkin]

	_co = isCommander; _Superior = bIsAiSuperior
	_biasCo = 0
	? not _co && _Superior: _biasCo = _COcount
	? not _co && not _Superior && (0 == _val || 3 == _val): _val = 1, goto "OnHighlightChange"

	? call dlgUpdated: goto "Quit"
	? !(alive player): closeDialog 0; goto "Quit"

	? (btnAction0): btnAction0=false; goto "SetBtn"
	? (btnAction1): btnAction1=false; goto "Clear"
	? (btnAction2): btnAction2=false; goto "ClearAllPlayer"
	? (btnAction3): btnAction3=false; goto "ClearAllCO"
	? (btnAction4): btnAction4=false; goto "OrderAI"

	? btnValue02 >= 0: _val = btnValue02, btnValue02 = -1, goto "OnHighlightChange"

	? bNewCoSet: bNewCoSet = false; call _refreshList

	~ 0.2
	goto "Update"

# OnHighlightChange
	; _val is both the value of btnValue02 and the index of indexWayPointSelected
	_bValid = false
	? 0 == _val: if (_co || _Superior) then {_highlight = "CO", _bValid = true}
	? 1 == _val: _highlight = "WP", _bValid = true
	? 2 == _val: _highlight = "PlayerTarget", _bValid = true
	? 3 == _val: if (_co || _Superior) then {_highlight = "ServerTarget", _bValid = true}
	? _bValid: call _refreshList, {ctrlShow [_idcBtnBg_0_0 + _x, false]} forEach [0,1,2,3], ctrlShow [_idcBtnBg_0_0 + _val, true]
	goto "Update"

# SetBtn
	_sel = lbCurSel _idcList
	? _sel == -1: hint "No waypoint selected"; goto "Update"
	indexWayPointSelected set [_val, _sel]
	_coor = _pos

	? "CO" == _highlight: [siPlayer, giPlayer, _coor, 0, _sel + _biasCo] exec "Common\Msg\sSetWPCO.sqs"
	? "WP" == _highlight: [_sel, _coor] exec "\TZK_Scripts_4_0_4\Player\SetWaypoint.sqs"
	? "PlayerTarget" == _highlight: [_sel, _pos, true] exec _setTargetScript
	? "ServerTarget" == _highlight: [siPlayer, giPlayer, _coor, 1, _sel] exec "Common\Msg\sSetWPCO.sqs"

	closeDialog 0
	goto "Quit"
	
# Clear
	_sel = lbCurSel _idcList
	? _sel == -1: hint "No waypoint selected"; goto "Update"
	indexWayPointSelected set [_val, _sel]
	_coor = [-1, -1]

	? "CO" == _highlight: [siPlayer, giPlayer, _coor, 0, _sel + _biasCo] exec "Common\Msg\sSetWPCO.sqs"
	? "WP" == _highlight: [_sel, _coor] exec "\TZK_Scripts_4_0_4\Player\SetWaypoint.sqs"
	? "PlayerTarget" == _highlight: [_sel, _pos, true] exec _setTargetScript
	? "ServerTarget" == _highlight: [siPlayer, giPlayer, _coor, 1, _sel] exec "Common\Msg\sSetWPCO.sqs"

	~ 0.2
	goto "Update"

# ClearAllPlayer
	indexWayPointSelected set [1, -1]
	_i = 0; while {_i < countWPPlayer} do {[_i, [-1,-1]] exec "\TZK_Scripts_4_0_4\Player\SetWaypoint.sqs", _i = _i + 1}
	call _refreshList
	goto "Update"

# ClearAllCO
	? "CO" == _highlight: [siPlayer, giPlayer, [-1, -1], 0, -1] exec "Common\Msg\sSetWPCO.sqs"
	~ 0.2
	goto "Update"

# OrderAI
	closeDialog 0
	~0.1
	[] exec localize {TZK_DIALOG_AI_GRP_ORDER}
	goto "Quit"

# Quit
	[false, _id + 2] exec "Player\Rts\SetRtsMouse.sqs"; exit