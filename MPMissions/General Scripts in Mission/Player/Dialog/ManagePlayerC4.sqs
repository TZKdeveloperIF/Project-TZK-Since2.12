; args: player
_unit = player; _distLimit = 1000;

@!mutexDlg
mutexDlg = true; dlgID = dlgID + 1; _dlgID = dlgID
_dlg = createDialog "GenericDialog"
mutexDlg = false

_idcAction0Label = IDC+0
_idcAction0 = IDC+1
_idcAction1Label = IDC+2
_idcAction1 = IDC+3
_idcAction2Label = IDC+4
_idcAction2 = IDC+5
_idcAction3Label = IDC+6
_idcAction3 = IDC+7
_idcAction4Label = IDC+8
_idcAction4 = IDC+9

_idcListHeader = IDC+10
_idcList = IDC+11

btnAction0 = false
btnAction1 = false
btnAction2 = false
btnAction3 = false
btnAction4 = false

{ ctrlShow [_x, false] } forEach [ _idcAction0Label, _idcAction1Label, _idcAction2Label, _idcAction3Label, _idcAction4Label ]
{ ctrlShow [_x, false] } forEach [_idcAction2, _idcAction3, _idcAction4]

ctrlSetText [IDC_TITLE, "Manage Player C4"]
ctrlSetText [_idcAction0, "Trigger selected"]
ctrlSetText [_idcAction1, "Trigger All"]
ctrlSetText [_idcAction2, ""]
ctrlSetText [_idcListHeader, ""]

lbSetCurSel [_idcList, -1]
_lambdaForceRefresh = {_loopCnt = 5}; _lambdaNextRefresh = {_loopCnt = 0}; _loopCnt = 0; call _lambdaForceRefresh

# Update
ctrlShow [IDC_DialogBG_CR, !bool_TZK_DarkSkin]; ctrlShow [IDC_DialogBG_Light, bool_TZK_DarkSkin]
_loopCnt = _loopCnt + 1
	? call dlgUpdated: exit
	? !alive player: closeDialog 0; exit

	? _loopCnt > 5: call _lambdaNextRefresh, call preprocessFile "Player\Dialog\MPC_List.sqf"
		
	? btnAction0: btnAction0 = false; goto "TriggerSelected"
	? btnAction1: btnAction1 = false; goto "TriggerAll"

	~0.2
	goto "Update"

# TriggerSelected
	_i = lbCurSel _idcList;
	? _i < 0 || _i >= count Array_TZK_PlayerC4: goto "Update"
	_bomb = Array_TZK_PlayerC4 select _i
	? isNull _bomb: goto "Update"
	_pos = getPosASL _bomb; deleteVehicle _bomb; [_unit, _pos] exec "Util\CreateSatchelShell.sqs"
	~0.1
	call _lambdaForceRefresh
	goto "Update"
  
# TriggerAll
	closeDialog 0
	_i = 0; _c = count Array_TZK_PlayerC4
	? 0 == _c: exit
	# TriggerAllLoop
		_bomb = Array_TZK_PlayerC4 select _i
		? isNull _bomb: goto "TriggerAllContinue"
		_pos = getPosASL _bomb; deleteVehicle _bomb; [_unit, _pos] exec "Util\CreateSatchelShell.sqs"
		# TriggerAllContinue
		_i = _i + 1
		? _i < _c: goto "TriggerAllLoop"