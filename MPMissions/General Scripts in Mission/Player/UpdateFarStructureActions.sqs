; args: [[object, typeStructure, si], ["action1", "action2", ...], actionDistance]

_object = (_this select 0) select 0
_type = (_this select 0) select 1
_si = (_this select 0) select 2
_actionNames = _this select 1
_actionDistance = _this select 2
_this = 0

_actionIDs = []; _actionsAdded = false
_unit = player; _delay = 0.5
_structName = structDefs select _type select sdName

# Update
	~_delay

	? (isNull _object): goto "RemoveActionsAndExit"
	? !(alive _object): goto "RemoveActionsAndWaitUntilRepaired"
	? !(alive _unit): goto "RemoveActionsAndWaitUntilAlive"

	; add/remove actions
	_delay = 10
	? (_unit distance _object < 100+_actionDistance): _delay = 0.5
	? (!_actionsAdded) && (_unit distance _object <= _actionDistance): goto "AddActions"
	? (_actionsAdded) && (_unit distance _object > _actionDistance): goto "RemoveActions"

	? (_actionsAdded && structAction in _actionIDs): goto "PerformAction"
	
	goto "Update"

# AddActions
	{ _id = _unit AddAction [_x, "\TZK_Scripts_4_0_4\Player\ActionStructure.sqs"]; _actionIDs = _actionIDs + [_id] } foreach _actionNames
	_actionsAdded = true
	goto "Update"

# RemoveActions
	{ _unit removeAction _x } foreach _actionIDs
	_actionIDs = []; _actionsAdded = false
	goto "Update"

# RemoveActionsAndExit
	{ _unit removeAction _x } foreach _actionIDs
	_actionIDs = []; _actionsAdded = false
	exit

# RemoveActionsAndWaitUntilAlive
	{ _unit removeAction _x } foreach _actionIDs
	_actionIDs = []; _actionsAdded = false
	@ alive player
	_unit = player; _delay = 0
	goto "Update"

# RemoveActionsAndWaitUntilRepaired
	{ _unit removeAction _x } foreach _actionIDs
	_actionIDs = []; _actionsAdded = false
	@ isNull _object || alive _object
	;player globalchat "@(isNull _object || alive _object) passed"
	goto "Update"

# PerformAction
	_index = 0; _count = count _actionIDs
	# FindAction
		? structAction == _actionIDs select _index: goto "ActionFound"
		_index = _index + 1
		? _index < _count: goto "FindAction"

	structAction = -1
	"ERROR: action not found in UpdateFarStructureActions.sqs" call fDebugLog
	goto "Update"

	# ActionFound

	structAction = -1
	_script = format ["Player\ActionFar\%1.sqs", _actionNames select _index]
	[_object, _type, _si] exec _script
	goto "Update"