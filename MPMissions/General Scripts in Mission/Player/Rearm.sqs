; old args: [unit, truck, <vehicle>, <air support>]
; new args: [[unit, truck, <vehicle>, <air support>], order finished, rearm succeeded]

; modified parameters. Compatible with old format
_param = _this
? not ((_param select 0) in [_param select 0]): _param = _this select 0

_unit = _param select 0
_support = _param select 1
_vehicle = if (count _param > 2) then {_param select 2} else {vehicle _unit}
_bAirSup = if (count _param > 3) then {_param select 3} else {false}

_range = if !_bAirSup then {2*rangeSupport} else {rangeSupportPlane}

_msg = ""
? (_vehicle distance _support) > _range: _msg = "Support vehicle out of range"; goto "Quit"

? _unit == player && _unit == _vehicle: [_unit, _support, false, 1] exec "\TZK_Scripts_4_0_4\Player\Dialog\Equipment.sqs"; goto "MsgAndQuit"

_timeRearm = 30;
? _unit == _vehicle: _timeRearm = 5
? _vehicle in vehiclesRearming: _msg = "Rearm not started, rearm of vehicle already in progress."; goto "MsgAndQuit"

_enum = -1
; get "_enum" if vehicle is "Custom Weapon Vehicle"
? _unit != _vehicle: _vehicle call loadFile "Common\SQF\GetCustomWeaponVehicle.sqf"
? _enum != -1: if (dialog) then {closeDialog 0}, ["TZK_CustomAircraftWeaponDialog2", _enum, _vehicle, _support] exec "Cwv\Dialog.sqs", goto "MsgAndQuit"

; player globalchat format["_type=%1  unit: %2", _type, (unitDefs select _type) select udName ]

_type = _vehicle call funcGetUnitTypeFromObject
? (_type in vDoubleRearmTime): _timeRearm = 30*2
? (_type in vTripleRearmTime): _timeRearm = 30*3
? (_type in vSixfoldRearmTime): _timeRearm = 30*6
? _type == typesRocketLauncher select si0: _timeRearm = 144
? _type == typesRocketLauncher select si1: _timeRearm = 300
? dev: _timeRearm = 5
? bool_TZK_MF_Mode && _timeRearm > 30: _timeRearm = 30


? _type == -1: goto "CheckStruct"
_cost = (unitDefs select _type) select udCost
goto "CheckData"

# CheckStruct
_type = _vehicle call funcGetStructTypeFromObject
? _type == -1: _msg = format ["ERROR: object type not found for %1/%2 in Rearm.sqs", _vehicle, typeOf _vehicle]; goto "MsgAndQuit"
_cost = (structDefs select _type) select sdCost

#CheckData
; ver TZK_4.0.5.10, obtain rearmData directly for soldier member.
_weapons = []; _mags = [];
? _unit == _vehicle: _i = rearmDataObj find _unit; if (_i != -1) then {_weapons = rearmDataEquip select _i select 0; _mags = rearmDataEquip select _i select 1; goto "DataChecked"}

_rearmData = _vehicle call funcGetRearmData
_weapons = _rearmData select 0
_mags = _rearmData select 1
    #DataChecked
    ? (count _mags) == 0: _msg = "Unit is not armed."; goto "Quit"
    ? !(canFire _vehicle): _unit groupChat "Gun barrel badly hurt. Better repair as well."

; CALC REARM COST
_money = groupMoneyMatrix select siPlayer select giPlayer

? _unit != _vehicle: goto "RearmVehicle"

#RearmInfantry
	_cost = [_unit, _weapons, _mags] call funcCalcRearmCost
	? _cost > _money: _msg = format["Not enough money for rearm. ($%1 needed)", _cost]; goto "Quit"
	
	? !(local _vehicle): _msg = "Rearm failed. Soldier is not local."; goto "Quit"
  ; TODO _vehicle setCombatMode "BLUE"
  ~_timeRearm
	? !(alive _vehicle): _msg = ""; goto "Quit"
	; TODO ? !(alive _support): _msg = "Rearm failed, support vehicle destroyed."; _vehicle setCombatMode "YELLOW"; goto "Quit"
	? !(alive _support): _msg = "Rearm failed, support vehicle destroyed."; goto "Quit"
  removeAllWeapons _vehicle
  ; add custom throw/put
  { _vehicle addWeapon _x } forEach weaponsCustom
  ? count (magazines _vehicle) > 0: format["ERROR: soldier '%1-%2' has mags in Rearm.sqs", _vehicle, typeOf _vehicle] call fDebugLog; goto "MsgAndQuit"
  { _vehicle addMagazine _x } foreach _mags
  ? count (weapons _vehicle) > 0: format["ERROR: soldier '%1-%2' has weapons in Rearm.sqs", _vehicle, typeOf _vehicle] call fDebugLog; goto "MsgAndQuit"
  { _vehicle addWeapon _x } foreach _weapons
  ; TODO _vehicle setCombatMode "YELLOW"
  goto "RearmEnd"

#RearmVehicle
	_cost = _cost/10
	_cost = _cost - (_cost % 1)
	? _cost < 50: _cost = 50
	? _cost > _money: _msg = format["Not enough money for rearm. ($%1 needed)", _cost]; goto "Quit"

	[_vehicle] exec localize {TZK_NETWORK_REMOVE_MAG}
	; Force howitzer and tos deploy in 2.01 game
	_vehicle call preprocessFile "Util\DeployInRearm.sqf"

	vehiclesRearming set [count vehiclesRearming, _vehicle]
	_msg = format["Rearm started, ready in %1s ...", _timeRearm]
	? _unit == player: hint _msg
	_unit groupChat _msg
	_timeComplete = time + _timeRearm
	_text = format ["Rearming. %1 seconds left.", (time - _timeComplete) - (time - _timeComplete)%0.1 + 0.1]
	? player in _vehicle: hint _text
	_range = if !_bAirSup then {4*rangeSupport} else {2*rangeSupportPlane}
	#Loop
		_vehicle say "Rearming_xj400";
		~5
		? !(alive _vehicle): _msg = "Rearm failed. Vehicle destroyed."; goto "Quit"
		? !(alive _support): _msg = "Rearm failed. Support vehicle destroyed."; goto "Quit"
		? (_vehicle distance _support) > _range: if (([positionAttach, (getPos _vehicle)] call funcDistH) > 100 && ([positionAttach, (getPos _support)] call funcDistH) > 100) then {_msg = "Rearm failed. Support vehicle out of range."; goto "Quit"}
		_text = format ["Rearming. %1 seconds left.", (time - _timeComplete) - (time - _timeComplete)%0.1 + 0.1]
		? player in _vehicle: hint _text
		? time < _timeComplete: goto "Loop"

  ? _vehicle call preprocessFile "Util\NoUseableMag.sqf": [_vehicle] exec localize {TZK_NETWORK_SEND_REARM_VEH}
  ? (_type in vSixfoldRearmTime): _vehicle say "NuclearMissileReady_xj400"

# RearmEnd
	? _cost > 0: [_cost] exec "\TZK_Scripts_4_0_4\Player\SendMoneySpent.sqs"
	_msg = format["Rearm Complete. $%1", _cost]
	_this set [2, true]

# Quit
	? _unit != _vehicle: [vehiclesRearming, [_vehicle]] call preprocessFile "Util\ArrayErase.sqf"
# MsgAndQuit
	? _msg != "": if (_unit == player) then {hint _msg} else {_unit groupChat _msg}
	_this set [1, true]