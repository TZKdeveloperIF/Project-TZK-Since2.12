; args: [unit, truck, <vehicle>, <air support>]

_unit = _this select 0
_support = _this select 1
_vehicle = if (count _this > 2) then {_this select 2} else {vehicle _unit}
_bAirSup = if (count _this > 3) then {_this select 3} else {false}
_this = 0
_range = if !_bAirSup then {2*rangeSupport} else {rangeSupportPlane}

? (_vehicle distance _support) > _range: _msg = "Support vehicle out of range"; goto "Exit"

? _unit == player && _unit == _vehicle: [_unit, _support, false, 1] exec "\TZK_Scripts_4_0_4\Player\Dialog\Equipment.sqs"; exit

_msg = ""; _timeRearm = 30;
? _unit == _vehicle: _timeRearm = 5
? _vehicle in vehiclesRearming: _msg = "Rearm not started, rearm of vehicle already in progress."; if (_unit == player) then {hint _msg}; _unit groupChat _msg; exit

_enum = -1
; get "_enum" if vehicle is "Custom Weapon Vehicle"
? _unit != _vehicle: _vehicle call loadFile "Common\SQF\GetCustomWeaponVehicle.sqf"
? _enum != -1: if (dialog) then {closeDialog 0}, ["TZK_CustomAircraftWeaponDialog2", _enum, _vehicle, _support] exec "Cwv\Dialog.sqs", exit

; player globalchat format["_type=%1  unit: %2", _type, (unitDefs select _type) select udName ]

_type = _vehicle call funcGetUnitTypeFromObject
? (_type in vDoubleRearmTime): _timeRearm = 30*2
? (_type in vTripleRearmTime): _timeRearm = 30*3
? (_type in vSixfoldRearmTime): _timeRearm = 30*6
? (_type in vTenfoldRearmTime): _timeRearm = 30*10
? dev: _timeRearm = 5
? bool_TZK_MF_Mode && _timeRearm > 30: _timeRearm = 30


? _type == -1: goto "CheckStruct"
; TODO ? _type == (utCustom select siPlayer) && _unit == _vehicle: [_unit, _support, false, 1] exec "\TZK_Scripts_4_0_4\Player\Dialog\Equipment.sqs"; exit
_cost = (unitDefs select _type) select udCost
goto "CheckData"

#CheckStruct
_type = _vehicle call funcGetStructTypeFromObject
? _type == -1: format["ERROR: object type not found for %1/%2 in Rearm.sqs", _vehicle, typeOf _vehicle] call fDebugLog; exit
_cost = (structDefs select _type) select sdCost

#CheckData
; ver TZK_4.0.5.10, obtain rearmData directly for soldier member.
_weapons = []; _mags = [];
? _unit == _vehicle: _i = rearmDataObj find _unit; if (_i != -1) then {_weapons = rearmDataEquip select _i select 0; _mags = rearmDataEquip select _i select 1; goto "DataChecked"}

_rearmData = _vehicle call funcGetRearmData
_weapons = _rearmData select 0
_mags = _rearmData select 1
    #DataChecked
    ? (count _mags) == 0: _msg = "Unit is not armed."; goto "Exit"
    ? !(canFire _vehicle): _unit groupChat "Gun barrel badly hurt. Better repair as well."

; CALC REARM COST
_money = groupMoneyMatrix select siPlayer select giPlayer

? _unit != _vehicle: goto "RearmVehicle"

#RearmInfantry
	_cost = [_unit, _weapons, _mags] call funcCalcRearmCost
	? _cost > _money: _msg = format["Not enough money for rearm. ($%1 needed)", _cost]; goto "Exit"
	
	? !(local _vehicle): _msg = "Rearm failed. Soldier is not local."; goto "Exit"
  ; TODO _vehicle setCombatMode "BLUE"
  ~_timeRearm
	? !(alive _vehicle): _msg = ""; goto "Exit"
	; TODO ? !(alive _support): _msg = "Rearm failed, support vehicle destroyed."; _vehicle setCombatMode "YELLOW"; goto "Exit"
	? !(alive _support): _msg = "Rearm failed, support vehicle destroyed."; goto "Exit"
  removeAllWeapons _vehicle
  ; add custom throw/put
  { _vehicle addWeapon _x } forEach weaponsCustom
  ? count (magazines _vehicle) > 0: format["ERROR: soldier '%1-%2' has mags in Rearm.sqs", _vehicle, typeOf _vehicle] call fDebugLog; exit
  { _vehicle addMagazine _x } foreach _mags
  ? count (weapons _vehicle) > 0: format["ERROR: soldier '%1-%2' has weapons in Rearm.sqs", _vehicle, typeOf _vehicle] call fDebugLog; exit
  { _vehicle addWeapon _x } foreach _weapons
  ; TODO _vehicle setCombatMode "YELLOW"
  goto "RearmEnd"

#RearmVehicle
	_cost = _cost/10
	_cost = _cost - (_cost % 1)
	? _cost < 50: _cost = 50
	? _cost > _money: _msg = format["Not enough money for rearm. ($%1 needed)", _cost]; goto "Exit"

	[_vehicle] exec localize {TZK_NETWORK_REMOVE_MAG}
	; Force howitzer and tos deploy in 2.01 game
	_vehicle call preprocessFile "Util\DeployInRearm.sqf"

	vehiclesRearming set [count vehiclesRearming, _vehicle]
	_msg = format["Rearm started, ready in %1s ...", _timeRearm]
	? _unit == player: hint _msg
	_unit groupChat _msg
	_timeComplete = time + _timeRearm
	_text = format ["Rearming. %1 seconds left.", (time - _timeComplete) - (time - _timeComplete)%0.1 + 0.1]
	? player in _vehicle: hint _text
	_range = if !_bAirSup then {4*rangeSupport} else {2*rangeSupportPlane}
	#Loop
		_vehicle say "Rearming_xj400";
		~5
		? !(alive _vehicle): _msg = "Rearm failed. Vehicle destroyed."; goto "Exit"
		? !(alive _support): _msg = "Rearm failed. Support vehicle destroyed."; goto "Exit"
		? (_vehicle distance _support) > _range: if (([positionAttach, (getPos _vehicle)] call funcDistH) > 100 && ([positionAttach, (getPos _support)] call funcDistH) > 100) then {_msg = "Rearm failed. Support vehicle out of range."; goto "Exit"}
		_text = format ["Rearming. %1 seconds left.", (time - _timeComplete) - (time - _timeComplete)%0.1 + 0.1]
		? player in _vehicle: hint _text
		? time < _timeComplete: goto "Loop"

  ? _vehicle call preprocessFile "Util\NoUseableMag.sqf": [_vehicle] exec localize {TZK_NETWORK_SEND_REARM_VEH}
  ? (_type in vSixfoldRearmTime): _vehicle say "NuclearMissileReady_xj400"

#RearmEnd
  ? _cost > 0: [_cost] exec "\TZK_Scripts_4_0_4\Player\SendMoneySpent.sqs"
  _msg = format["Rearm Complete. $%1", _cost]

#Exit
  ? _msg != "" && _unit == player: hint _msg
  ? _msg != "": _unit groupChat _msg
	? _unit != _vehicle: vehiclesRearming = vehiclesRearming - [_vehicle]
	exit
