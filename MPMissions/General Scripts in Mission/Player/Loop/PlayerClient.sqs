_unit = player
; Init
[_unit, siPlayer, 200] exec (TzkScripts select 039)
[_unit] exec "\TZK_Scripts_4_0_4\Player\AddPlayerActions.sqs"

	; init timeLastPing to current time if haven't received ping from server
	? timeLastPing < 0: timeLastPing = time

; The AnimChanged-EH is available since 1.99
	?!bool_TZK_Vanilla_Mode: _unit addEventHandler ["AnimChanged", { _this exec "\TZK_Scripts_4_0_4\Player\Event_AnimChanged_WW4.sqs" }]
	_unit addEventHandler ["Fired",{ _this exec (TzkScripts select 293) }]

_sensorOld = "EmptyDetector" camCreate getPos player; _sensorNow = "EmptyDetector" camCreate getPos player
_timeOld = -3600; _timeNow = -3600
_nextMusic = 5*60; _bool_TZK_Music = bool_TZK_Music

_posOld = getPos _sensorOld; _posNow = getPos _sensorNow
_unitsOld = units group player; _unitsNew = []
; Music list picked up by MF
_songs = ["Track1","Track2","Track3","Track5","Track6","Track7","Track8","Track11","Track15","RTrack1a","RTrack4","RTrack5"]
; out date duration of "invalid rts single click (red cross)" and "selecting empty area"
_rtsCrossOutdateDur = 10; _rtsAreaOutDateDur = 30;

# Update
	~1
	? TzkMapSelectClicked && time > TzkMapFirstGuideLineDrawnTime + _rtsCrossOutdateDur: call preprocessFile "Player\Rts\ResetGuideLines.sqf"
	? not TzkMapSelectClicked && TzkMapAreaLastUsedTime > 0 && time > TzkMapAreaLastUsedTime + _rtsAreaOutDateDur && 0 == count (call preprocessFile "Rts\Ui\CurSelUnitArray.sqf"): false call preprocessFile "Player\Rts\Area.sqf"; false call preprocessFile "Player\Rts\Selection.sqf";

	_mhq = mhq select siPlayer
	_mcvs = MCV select siPlayer

	? pvGameOver != -1: [] exec (TzkScripts select 191); [] exec (TzkScripts select 229); Exit

	? time > (timeLastPing + blockTime) && !bBlockedByClient: bBlockedByClient = true; 0 exec "Player\BlockByClient.sqs"
	
	? !alive _unit: goto "WaitUntilAlive"

	_veh = vehicle player
	_posNow = getPos player; _sensorNow setPos _posNow; _timeNow = time
	_distance = [_posOld, _posNow] call funcDistH
	? ([_posOld, positionRespawn] call funcDistH) < 100 || ([positionRespawn, _posNow] call funcDistH) < 100: _distance = 0
	_bMoveCheat = _timeNow - _timeOld > 0.5 && _distance / (_timeNow - _timeOld) > 600
	? _bMoveCheat: _bMoveCheat = !(_veh in TzkDraggedVehs)
	? _bMoveCheat: _veh setdamage 1; hint "Big brother is watching you."; player globalChat "Big brother is watching you."
	_posOld = _posNow; _sensorOld setPos _posOld; _timeOld = _timeNow

	_unitsNew resize 0
	{if (_x != player && _unitsOld find _x == -1) then {_unitsNew set [count _unitsNew, _x]}} forEach units group player
	{[_x] exec "\TZK_Scripts_4_0_4\Player\UpdateDestroyEnemyObject.sqs"; doStop _x} forEach _unitsNew
	_i = 0; _j = 0; _c1 = count _unitsOld; _c2 = count _unitsNew
	while {_i < _c1 && _j < _c2} do {																									if (!alive(_unitsOld select _i)) then {_unitsOld set [_i, _unitsNew select _j]; _j = _j + 1};									_i = _i + 1;																												  }
	while {_j < _c2} do {_unitsOld set [count _unitsOld, _unitsNew select _j]; _j = _j + 1}
	
	? ((groupPlayer == (groupCommander select siPlayer)) && !isCommander): isCommander = true
	? ((groupPlayer != (groupCommander select siPlayer)) && isCommander): isCommander = false
	
	_mhqPos = getPos _mhq
	_mhqNearby = [false, true] select ( ([_posNow, _mhqPos] call funcDistH) < 150 ); if (!alive _mhq) then {_mhqNearby = false}
	? !_mhqNearby: if (abs (((getPosASL _mhq) select 0) - TzkAttachPosX) > 100) then {mhqNearby = false}
	? _mhqNearby: mhqNearby = true
; 	mhqNearby = [false, true] select ( ( ([_posNow, siPlayer, [[utMHQ0],[utMHQ1]] select siPlayer, []] call funcGetClosestVehicle) select 1 ) < 150 )
	MCVNearby = [false, true] select ( ( ([_posNow, siPlayer, [[utMCVW],[utMCVE]] select siPlayer, []] call funcGetClosestVehicle) select 1 ) < 150 )
	SupNearby = [false, true] select ( ( ([_posNow, siPlayer, typesSupportPlus select siPlayer, []] call funcGetClosestVehicle) select 1 ) < 50 )
	? mhqNearby: MCVNearby = false; SupNearby = false

	? -1 != (_veh) call funcGetUnitTypeFromObject && !local _veh: if (player == commander _veh && !dev) then {player action ["Eject", _veh]}
	
	setViewDistance viewDistance; showGPS boole_Show_GPS; showPad boole_Show_Pad; showRadio bool_Show_Radio
	
	? !bool_TZK_Music && _bool_TZK_Music: 1 FadeMusic 0, _nextMusic = 0
	? bool_TZK_Music && !_bool_TZK_Music: PlayMusic [_songs Select Random ((Count _songs) - 1),0], 0 FadeMusic 1, _nextMusic = time + 400 + Random 400
	? bool_TZK_Music && _bool_TZK_Music: if (time > _nextMusic) Then {PlayMusic [_songs Select Random ((Count _songs) - 1),0], 0 FadeMusic 1, _nextMusic = time + 400 + Random 400}
	_bool_TZK_Music = bool_TZK_Music
	goto "Update"


# WaitUntilAlive
	_timeRespawn = time + 30
	
	forceMap true

	closeDialog 0
	~ 1
	[_timeRespawn] exec "\TZK_Scripts_4_0_4\Player\Dialog\Respawn.sqs"
	
	_index = 0
	# DisableActions
		_unit removeAction _index
		_index = _index + 1
		? _index < 10: goto "DisableActions"

	; cancel forceMap before respawn
	@ alive player || pvGameOver != -1 || time > _timeRespawn - 3
	forceMap false

	@ alive player || pvGameOver != -1

	? pvGameOver != -1: [] exec (TzkScripts select 191); [] exec (TzkScripts select 229); exit

	_unit = player
	removeAllWeapons _unit
	[_unit, siPlayer, 200] exec (TzkScripts select 039)

	_money = groupMoneyMatrix select siPlayer select giPlayer

	? _money >= costRespawn: goto "Respawn"
	#WaitMoney
		_unit setPos [-2000 + _si*100, -2000 + _gi*100, 1000]
		_unit setVelocity [0,0,0]
		~1
		_money = groupMoneyMatrix select siPlayer select giPlayer
		? _money < costRespawn: goto "WaitMoney"
	
	#Respawn
		? costRespawn > 0: [costRespawn] exec "Net\sMoneySpent.sqs"
		? !(alive objectRespawn): objectRespawn = [objectRespawn, siPlayer] call funcGetNearestRespawnObject

		_ts = objectRespawn call funcGetStructTypeFromObject
		? _ts != -1: goto "RespawnStruct"
	
	#RespawnMHQMCV
		_dir = getDir objectRespawn
		_pos = getPos objectRespawn
		_posRespawn = [(_pos select 0)-(sin _dir)*10,(_pos select 1)-(cos _dir)*10,0]
		? !(alive objectRespawn) || isNull objectRespawn: _posRespawn = positionRespawn
		goto "RespawnPosSet"
  
	#RespawnStruct
		_res = [objectRespawn, _ts, siPlayer] call funcCalcUnitPlacementPosDir
		_posRespawn = _res select 0
		? !(alive objectRespawn) || isNull objectRespawn: _posRespawn = positionRespawn
	
	#RespawnPosSet
		? (_posRespawn select 2) < 3: _posRespawn set [2, 3]
		_unit setVelocity [0,0,0]
		_unit setPos _posRespawn

	forceMap false

	[_unit, siPlayer] exec (TzkScripts select 280)

	[_unit] exec "\TZK_Scripts_4_0_4\Player\AddPlayerActions.sqs"

	; The AnimChanged-EH is available since 1.99
	?!bool_TZK_Vanilla_Mode: _unit addEventHandler ["AnimChanged", { _this exec "\TZK_Scripts_4_0_4\Player\Event_AnimChanged_WW4.sqs" }]
	_unit addEventHandler ["Fired",{ _this exec (TzkScripts select 293) }]

	_posOld = _posRespawn
	_posNow = _posRespawn
	goto "Update"

