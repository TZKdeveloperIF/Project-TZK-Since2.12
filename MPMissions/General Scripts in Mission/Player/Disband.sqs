; args: units
_units = _this
? count _units > 1 : _unitsToRemove = []; {if !(_x in units player) then { _unitsToRemove set [count _unitsToRemove, _x] } } forEach _units; _units = _units - _unitsToRemove

; commander uses "black list". Town groups can't be disband
; Since RTS system allows CMD disband other players' members, the UC disband blacklist shouldn't limit this as well
; common players use "white list". Only groups he can manage can be disband
_whileList = [groupPlayer]
_blackList = (townGroups select siPlayer)
_playableGroups = (groupMatrix select si0) + (groupMatrix select si1)
_workerAndBaseGroup = (workerGroups select siPlayer) + [baseGroup select siPlayer]
? (groupCommander select siPlayer) in (groupAiMatrix select siPlayer): _whileList = _whileList + (groupAiMatrix select siPlayer) + _workerAndBaseGroup, goto "SkipSuperiorCheck"
? giPlayer == giCO select siPlayer: goto "SkipSuperiorCheck"

_index = 0; _gis = []; _groupsAI = groupAiMatrix select siPlayer; _siGroups = groupMatrix select siPlayer; _groupNames = groupNameMatrix select siPlayer
{ if (_x in _groupsAI) then { _gis set [count _gis, _index] }; _index = _index + 1} foreach _siGroups
{ if ( (["Commander"] + callsigns) select (((aiSetting select siPlayer) select _x) select aisSuperior) == _groupNames select giPlayer ) then { _whileList = _whileList + [_siGroups select _x] } } forEach _gis

# SkipSuperiorCheck

_extraRecycler = []
_i = 0; _c = count _units; _multiDisband = _c > 1
# CheckUnit
	? !(_i < _c) : goto "Quit" 
	_u = _units select _i; _i = _i + 1
	; ? !(alive _u) : player groupchat "You can't disband dead units."; goto "CheckUnit"
	_crew = crew _u
	; player globalchat format["crew _u = %1", crew _u]
	? giPlayer != (giCO select siPlayer) && count _crew == 0 : player groupchat "Only commanders can disband empty vehicles."; goto "CheckUnit"

	? count _crew <= 0 || _u in _crew: goto "skipNonEmptyVehicleCheck"
	; non-empty vehicle
	; verify all crews. "join" sometimes creates bug and leave join group units in vehicle
	; allow remove vehicle and those units
		_bValid = true; _i = 0; _c = count _crew
		# vehicleCrewCheck
			_human = _crew select _i
			? not alive _human: goto "vccContinue"
			? sides find (side _human) != siPlayer: _bValid = false
			? (group _human) in _playableGroups: _bValid = false

			# vccContinue
			_i = _i + 1
			? _i < _c && _bValid: goto "vehicleCrewCheck"
		? not _bValid: player groupchat "You can't disband non-empty vehicles."; goto "CheckUnit"
		_extraRecycler = _extraRecycler + _crew

	# skipNonEmptyVehicleCheck

	; empty vehicle
	? count _crew == 0 && (_u in ( mhq + (MCV select si0) + (MCV select si1) )) : player groupchat "You can't disband MHQ/MCV."; goto "CheckUnit"
	? count _crew == 0 : _u exec "Player\DisbandEmptyVehicle.sqs"; goto "CheckUnit"

	; Don't allow disband enemy
	_enemySide = sides select (siEnemy select siPlayer)
	? giPlayer == giCO select siPlayer: if (side group _u == _enemySide) Then {player groupchat "You are not allowed to disband this unit."; goto "CheckUnit"}
	; check if unit is allowed to be disband. Since TZK_2.0 the "Join" is allowed but in MP it sometimes meets error, thus commander should be able to command more groups
	? giPlayer == giCO select siPlayer: if ((group _u) in _blackList) Then {player groupchat "You are not allowed to disband this unit."; goto "CheckUnit"}
	? giPlayer != giCO select siPlayer: if !((group _u) in _whileList) Then {player groupchat "You are not allowed to disband this unit."; goto "CheckUnit"}

	_delay = 1
	? player == leader _u: _delay = 0.1
	? not _multiDisband: player groupchat "Disbanding unit."
	? _u == vehicle _u && _u != leader _u: goto "SkipKill"
	_u setDamage 1
	~ _delay
	# SkipKill
	~_delay
	? _u == leader _u && !((group _u) in (_workerAndBaseGroup)) : player groupchat "Leader has been killed but won't be deleted."; goto "CheckUnit"
	deleteVehicle _u
	goto "CheckUnit"

# Quit
? _multiDisband: player groupchat format ["%1 units disbanded.", _c]
? count _extraRecycler > 0: {deleteVehicle _x} forEach _extraRecycler