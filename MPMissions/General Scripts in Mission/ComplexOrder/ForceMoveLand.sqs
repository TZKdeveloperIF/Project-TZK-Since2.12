; ---------------------------------------------------------------------------------------------------
; |								RTS Force Move Land Vehicle SQS Script								|
; |		  Vehicles on land will be forced to move via script command. 								|
; ---------------------------------------------------------------------------------------------------

; args: [
; 	[unit, Pos Info (read only), si],
; 	[order mode (called by AI or player unit), quit condition],
; 	extra parameters for order mode
; ]
_param = _this select 0

_unit = _param select 0
_destination = _param select 1
_si = _param select 2

_forServer = _this select 1 select 0, _quitCond = _this select 1 select 1
_orderQuitCond = {_unit == vehicle _unit || _unit != driver vehicle _unit}
? _forServer: _rtsStatus = _this select 2 select 0, _id = _this select 2 select 1
? not _forServer: _uid = _this select 2 select 0, _id = _this select 2 select 1

_msg = ""

? _unit == vehicle _unit: exit
? _unit != driver vehicle _unit: exit

_vehicle = vehicle _unit
_vehicle doWatch _destination
doStop _vehicle

~ 5 + random 5

? call _quitCond: goto "Quit"
? call _orderQuitCond: goto "Quit"

; execution
	_moveParamCache = [true, true, [_vehicle, _destination, 10]]
	_moveParamCache exec "Util\MoveVehUsingSetVel.sqs"
	# loopMoveLand
		~ 1
		? call _quitCond: _moveParamCache set [1, false], goto "Quit"
		? call _orderQuitCond: _moveParamCache set [1, false], goto "Quit"
		? _moveParamCache select 0: goto "loopMoveLand"

	_vehicle groupChat "Force move completed."

; End Of File
# Quit

	? _msg != "" && _forServer: [_si, format ["%1: %2", _unit, _msg]] exec "Server\Msg\sSvrMsg2Cmd.sqs"
	? _msg != "" && not _forServer: _unit groupChat _msg
	exit