; args: [si]
_si = _this select 0
? _si != si0 && _si != si1: exit

? not (call format [{pvWorkers%1 < maxWorkers select %1}, _si]): exit

; TZK once support MCV design but it's abandoned
_mhq = mhq select _si
? not alive _mhq: exit
_groups = workerGroups select _si
? count _groups == 0 : exit
call format [{pvWorkers%1 = pvWorkers%1 + 1; publicVariable "pvWorkers%1"}, _si]
_gi = giCO select _si
_ut = if (_si == si0) then {utWorkerW} else {utWorkerE}

[_si, _gi, costWorker] exec localize {TZK_MONEY_SERVER_SPEND}
_pos = getPos _mhq
? _pos select 0 > 0 && _pos select 1 > 0: goto "ObtainedPosition"
; try to locate mhq in-map
_try = 0
# ReadMhqPos
	~ 0.1
	_pos = getPos _mhq
	? _pos select 0 > 0 && _pos select 1 > 0: goto "ObtainedPosition"
	_try = _try + 1
	? _try < 10: goto "ReadMhqPos"
; Unable to locate mhq in-map. Do "MHQ attached check"
_i = 0; _c = count vehicleAttached
# CheckMHQTugged
	? _i >= _c: goto "ObtainedPosition"
	_entry = vehicleAttached select _i
	? _mhq in (_entry select tsTugged): _pos = getPos (_entry select tsTug), goto "ObtainedPosition"
	_i = _i + 1
	goto "CheckMHQTugged"

# ObtainedPosition

@ not mutexWorkerJoin
	mutexWorkerJoin = true
	_membersMin = 12; _i = 0; _j = 0
	{if (count(units _x) < _membersMin) then {_membersMin = count(units _x); _i = _j}; _j = _j + 1} forEach _groups
	[_ut, 0, 0, 0, _pos, 0, _si, giWorker, (_groups select _i), 0, 1] exec "\TZK_Scripts_4_0_4\Common\AddUnit.sqs"
	mutexWorkerJoin = false