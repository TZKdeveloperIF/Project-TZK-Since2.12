; args: [group, townIndex]

_group = _this select 0; _ti = _this select 1
_si = siRes

_inf = 3 + random 3
_tanks = 1 * restanksx
_interval = "(7 - restanksx) *60"
_patrol = 0

_distSpawn = viewDistance + 5000

_flag = (towns select _ti) select tdFlag
_posFlag = getPos _flag
? isNull _group : format["ERROR: _group is NULL at init for RES town group in '%1'", (towns select _ti) select tdName] call fDebugLog; exit

@!(mutexInitResGroups)
mutexInitResGroups = true
_list = groupMatrix select _si
_list set [count _list, _group]

mutexInitResGroups = false

~12
; wait patrol group initialized.
; Init Spawn Activate by Start Position
? (mhq select si0) distance _flag < _distSpawn || (mhq select si1) distance _flag < _distSpawn: goto "Spawn"

#CheckSpawn
	_spawn = false
	{ _g = _x; { if ((_x distance _flag) < _distSpawn) then { _spawn = true } } foreach (units _g) } foreach (groupMatrix select si0)
	{ _g = _x; { if ((_x distance _flag) < _distSpawn) then { _spawn = true } } foreach (units _g) } foreach (groupMatrix select si1)
	? _spawn: goto "Spawn"
	~10
	goto "CheckSpawn"

#Spawn
_infExtra = time/(call _interval); _infExtra = _infExtra - (_infExtra % 1)
_inf = _inf + _infExtra
? _inf > 8 : _tanks = _tanks + _inf - 8; _inf = 8
? _tanks > 2 : _patrol = _patrol + _tanks - 2; _tanks = 2
? isNull _group : format["ERROR: _group is NULL at spawn for RES town group in '%1'", (towns select _ti) select tdName] call fDebugLog

? _inf == 0 : goto "SpawnNoInf"
_i = 0
#SpawnAddInf
  _type = -1
  while "_type == -1" do { _type = (infTown select _si) call funcGetRandomUnitType }
  _pos = [_posFlag, 25, 50] call funcGetRandomPos
  [_type, 0, 0, 0, _pos, random 360, _si, -1, _group, 0, 0] exec (TzkScripts select 309)
  ~1
  _i = _i + 1
  ? _i < _inf : goto "SpawnAddInf"
#SpawnNoInf

? _tanks == 0 : goto "SpawnNoTanks"
_i = 0
#SpawnAddTank
		_type = -1
		while "_type == -1" do { _type = (airTown select _si) call funcGetRandomUnitType }
		_pos = [_posFlag, 25, 50] call funcGetRandomPos
		
		[_type, 1, 1, 0, _pos, random 360, _si, -1, _group, 0, 0] exec (TzkScripts select 309)
		~1
		
		
		
		
	_i = _i + 1
	? _i < _tanks : goto "SpawnAddTank"
#SpawnNoTanks

? _patrol == 0 : goto "SpawnNoPatrol"
_i = 0
#SpawnAddPatrol
	; find smallest patrol group
	@!(mutexAddResPatrol)
	mutexAddResPatrol = true
	_groupPatrol = grpNull
	_unitsMin = 12; _gi = 0; _giPatrol = 0
	{ _unitCount = (count units _x); if (_unitCount < _unitsMin) then { _groupPatrol = _x; _giPatrol = _gi; _unitsMin = _unitCount }; _gi = _gi + 1 } forEach groupsPatrol
	? (isNull _groupPatrol) || _unitsMin > 10 : mutexAddResPatrol = false; goto "SpawnNoPatrol"
		_type = -1
		while "_type == -1" do { _type = (airTown select _si) call funcGetRandomUnitType }
		_pos = [_posFlag, 150, 150] call funcGetRandomPos
		
		_vehicle = [_type, 1, 0, 0, _pos, random 360, _si, -1, _groupPatrol, 0, 0] call funcAddServerUnit
		
		_timeAbort = time + 5
		@ (!isNull _vehicle || time > _timeAbort)
		
	_i = _i + 1
	mutexAddResPatrol = false
	? _i < _patrol : goto "SpawnAddPatrol"
#SpawnNoPatrol

_timePatrol = 0
_timeReinforce = time + (call _interval)

#Update
  ~(25 + random 10)
	? time > _timeReinforce && ((towns select _ti) select tdSide) == siRes : goto "Reinforce"
	? (count (units _group)) == 0 : goto "Update"
	? ((towns Select _ti) select tdSide) != siRes : goto "TakeFlag"
	? time > _timePatrol : goto "Patrol"
	{ if ( alive _x && unitReady _x ) then { doStop _x } } foreach units _group
	{ if (alive _x) then { _x doWatch ([getPos _x, 200, 200] call funcGetRandomPos) } } foreach units _group
	goto "Update"

#Patrol
  _group setCombatMode "YELLOW"; _group setBehaviour "STEALTH"; _group setSpeedMode "NORMAL"

	_timePatrol = time + 3*60
	{ if ((random 1) < 0.5 && _x == driver vehicle _x) then { _x doMove ([getPos _flag, 150, 150] call funcGetRandomPos) } } foreach units _group
	goto "Update"

#TakeFlag
	{ if ((random 1) < 0.5 && _x == driver vehicle _x) then { _x doMove ([getPos _flag, 4, 4] call funcGetRandomPos) } } foreach units _group
	goto "Update"

#Reinforce
  _timeReinforce = time + (call _interval)
	? count (units _group) < 8 : goto "AddInf"
	? count (units _group) <= 9 : goto "AddTank"
	goto "SendOutTanks"

#AddInf
  _type = -1
  while "_type == -1" do { _type = (infTown select _si) call funcGetRandomUnitType }
  _pos = [_posFlag, 25, 50] call funcGetRandomPos
  [_type, 0, 0, 0, _pos, random 360, _si, -1, _group, 0, 0] exec (TzkScripts select 309)
  ; player globalchat format["AddInf: %1", (towns select _ti) select tdName]
  goto "Patrol"
  
#AddTank
  _type = -1
  while "_type == -1" do { _type = (airTown select _si) call funcGetRandomUnitType }
  _pos = [_posFlag, 12, 13] call funcGetRandomPos
  [_type, 1, 1, 0, _pos, random 360, _si, -1, _group, 0, 0] exec (TzkScripts select 309)
  ; player globalchat format["AddTank: %1", (towns select _ti) select tdName]
  goto "Patrol"

#SendOutTanks
	_tanksInGroup = []
	{ _v = vehicle _x; if (_x != _v && !(_v in _tanksInGroup) ) then { _tanksInGroup set [count _tanksInGroup, _v] } } forEach units _group
	; player globalchat format["SendOutTanks: %1", (towns select _ti) select tdName]
	_i = 0
	#NextTank
	@!(mutexAddResPatrol)
	mutexAddResPatrol = true
		_tank = _tanksInGroup select _i
		_groupPatrol = grpNull
		_unitsMin = 12; _gi = 0
		{ _unitCount = (count units _x); if (_unitCount < _unitsMin) then { _groupPatrol = _x; _unitsMin = _unitCount }; _gi = _gi + 1 } forEach groupsPatrol
		_units = units _group - (crew _tank)
		? _unitsMin <= 10 && !(isNull _groupPatrol) && count _units > 0 : (crew _tank) join _groupPatrol
		_i = _i + 1
		
	mutexAddResPatrol = false
    ? _i < (count _tanksInGroup) : goto "NextTank"
  goto "Patrol"