; args: [unit, init speed idx, curved idx]

_unit = _this select 0

_speed = ArtilleryMagazineSpeedList select (_this select 1)
_curved = [false, true] select (_this select 2)

? _speed > 500: _speed = 500

_vehicle = vehicle _unit
? _unit == _vehicle: exit
_gunner = gunner _vehicle
? _unit == _gunner: goto "Valid"
? not isNull _gunner: exit
_driver = driver _vehicle
? _unit == _driver: goto "Valid"
exit

# Valid
; must be vehicle gunner or driver with empty gunner seat
; OFP can process local vehicle without remote gunner or remote vehicle with local gunner
; structures are always local to server.

_vt = _vehicle call funcGetUnitTypeFromObject; _legalType = false
? _vt == -1: goto "CheckStructure"
	? _vt in typesHowitzer || _vt in typesRocketLauncher: _legalType = true
	goto "SwitchTheMags"
# CheckStructure
	_vt = _vehicle call funcGetStructTypeFromObject
	? _vt == -1: exit
	? _vt in structsOccupiableArtilleryMortar: if (_speed > 120) then {_speed = 120}; if !_curved then {_speed = 200}; _legalType = true
	? _vt in structsOccupiableHowitzer: _legalType = true

# SwitchTheMags
	? !_legalType: exit
	; hint format ["%1", [_speed, _type]]
	; Use Radio\Graduation script to switch the magazine. It has been well designed.
	[_vehicle, _speed] exec (TzkScripts select 132)

; EOF