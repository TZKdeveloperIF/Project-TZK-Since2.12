; args: [pos, player group selected units]
_pos = _this select 0, _units = _this select 1
@ !mutexDlg
	mutexDlg = true; dlgID = dlgID + 1; _dlgID = dlgID
	_dlg = createDialog "TzkCmdMapDlgPoint"

	RtsPriorityPointDialogEnum = 4
	RtsLatestPointDialogTime = time

mutexDlg = false

@ not mutexRtsMouse
	mutexRtsMouse = true
	_id = rtsMouseId
	mutexRtsMouse = false
[true, _id + 1, _pos] exec "Player\Rts\SetRtsMouse.sqs";

_pos call preprocessFile "Util\DebugPointInfo.sqf"

_idcLeftBtn0 = IDC + 16 * 2 + 0
_idcDownBtn0 = IDC + 16 * 17 + 0
; init right-down corner buttons (switching to other rts dialogs)
; 0: rts, 1: area, 2: wpco, 3: player order, 4: point
[4, _units] call preprocessFile "Rts\Dialog\InitSwitchDialogBtns.sqf"

btnValue00 = -1; btnValue01 = 0; btnValue03 = 0
btnFirstCtrl = false
ctrlSetText [IDC + 16 * 32 + 2, localize {TZK_LANG_WAYPOINT}]
btnFunction = 0

{ ctrlShow [_x, false] } forEach [_idcLeftBtn0]
? isCommander || bIsAiSuperior: ctrlSetText [_idcLeftBtn0, format ["%1 MHQ", localize {TZK_LANG_WALL}]]; ctrlShow [_idcLeftBtn0, true]
ctrlSetText [_idcLeftBtn0 + 1, localize {TZK_LANG_SURRENDER}]; ctrlShow [_idcLeftBtn0 + 1, isCommander]
ctrlSetText [_idcLeftBtn0 + 2, format ["%1 %2", localize {TZK_LANG_BASE}, localize {TZK_LANG_MORTAR}]]; ctrlShow [_idcLeftBtn0 + 2, isCommander]
ctrlSetText [_idcLeftBtn0 + 3, format ["%1 MHQ", localize {TZK_LANG_REPAIR}]]; ctrlShow [_idcLeftBtn0 + 3, isCommander]

; down board
_downBoardStack = []
call preprocessFile "Rts\Dialog\UpdateRtsPointDownBoard.sqf"

TzkMapRtsFlashingDisabled = true

# Update
	; ctrlShow [IDC_DialogBG_CR, !bool_TZK_DarkSkin]; ctrlShow [IDC_DialogBG_Light, bool_TZK_DarkSkin]
	? call dlgUpdated: goto "Quit"
	? not alive player: closeDialog 0; goto "Quit"
	call preprocessFile "Player\Rts\HandleDialogAzimut.sqf"

	? btnValue00 >= 0: _btnVal = btnValue00; btnValue00 = -1; goto "LeftBtn"
	? btnValue01 > 0: _btnVal = btnValue01; btnValue01 = 0; goto "DownBtn"
	? btnValue03 > 0: _btnVal = btnValue03; btnValue03 = 0; goto "RightDownBtn"
	? btnFirstCtrl: btnFirstCtrl = false; goto "FirstCtrl"
	? btnFunction > 0: _btnFnc = btnFunction, btnFunction = 0, goto "FunctionalBtns"

	; Skip
	~0.2
	goto "Update"
# Quit
	TzkMapRtsFlashingDisabled = false
	RtsLatestPointDialogTime = time
	[false, _id + 2] exec "Player\Rts\SetRtsMouse.sqs"; exit
# FirstCtrl
	; switch to waypoint dialog rapidly
	_btnVal = 2 ^ 2
	call preprocessFile "Rts\Dialog\SwitchDialogBehavior.sqf"; goto "Quit"
# LeftBtn
	? 0 == _btnVal: 0 exec "Rts\Building\sWallMhq.sqs"; goto "Update"
	? 1 == _btnVal: 0 exec "Rts\SubMenu\SurrenderConfirm.sqs"; closeDialog 0; goto "Quit"
	? 2 == _btnVal: [_pos, utMortar select siPlayer] exec "Rts\Deploy\sBaseSoldier.sqs"; closeDialog 0; goto "Quit"
	? 3 == _btnVal: call preprocessFile "Player\CheckRepairMhq.sqf"; closeDialog 0; goto "Quit"
	goto "Update"
# DownBtn
	? count _downBoardStack > 0: goto "DownSubMenu"
	? 2^0 == _btnVal: _pos exec "Rts\Building\sDeployBaseMg.sqs"; goto "Update";
	? 2^1 == _btnVal: _pos exec "Rts\SubMenu\BuildStruct.sqs"; closeDialog 0; goto "Quit"
	? 2^2 == _btnVal: _pos exec "Rts\SubMenu\BuyWorker.sqs"; closeDialog 0; goto "Quit"
	goto "Update"
	# DownSubMenu
		; ASSERT(3 == _downBoardStack select 0 && 1 == count _downBoardStack)
		~ 0.1
		goto "Update"
# RightDownBtn
	? _btnVal > 0: call preprocessFile "Rts\Dialog\SwitchDialogBehavior.sqf"; goto "Quit"
	goto "Update"
# FunctionalBtns
	? 1 == _btnFnc: 0 exec localize {TZK_DIALOG_SHOWNGROUPS}; closeDialog 0; goto "Quit"
	goto "Update"