; args: pos
_pos = _this
@ !mutexDlg
mutexDlg = true; dlgID = dlgID + 1; _dlgID = dlgID
_dlg = createDialog "TzkCmdMapDlgRts"
mutexDlg = false

@ not mutexRtsMouse
	mutexRtsMouse = true
	_id = rtsMouseId
	mutexRtsMouse = false
[true, _id + 1, _pos] exec "Player\Rts\SetRtsMouse.sqs";

_idcLeftBtn0 = IDC + 16 * 2 + 0
_idcDownBtn0 = IDC + 16 * 6 + 0
; init right-down corner buttons (switching to other rts dialogs)
; 0: rts, 1: area, 2: wpco, 4: point
0 call preprocessFile "Rts\Dialog\InitSwitchDialogBtns.sqf"

; init left-down corner buttons (select specific type of units)
_idcLeftDownBtn0 = IDC + 16 * 12 + 0
_cache0 = []; _cache1 = []; _cacheText1 = []; _btnVal = -1
_level = TzkSelStackIdx
; Remove dead units in selected array
_level call preprocessFile "Player\Rts\RemoveDeadSelectedUnit.sqf"
if (1 == _level) then {_btnVal = TzkSelLv1Btn}; if (2 == _level) then {_btnVal = TzkSelLv2Btn}
[_idcLeftDownBtn0, _level, _btnVal] call preprocessFile "Rts\Dialog\UpdateRtsDlgType.sqf"

btnValue00 = -1; btnValue01 = 0; btnValue02 = 0; btnValue03 = 0

; { ctrlShow [_x, false] } forEach []
ctrlSetText [_idcLeftBtn0, "Move"]
ctrlSetText [_idcLeftBtn0 + 1, "Clear Rts Order"]
ctrlSetText [_idcLeftBtn0 + 2, "Shoot Area"]
ctrlSetText [_idcLeftBtn0 + 3, "Move & Shoot"]
ctrlSetText [_idcLeftBtn0 + 4, "Board"]

ctrlSetText [_idcDownBtn0, "Disband"]
ctrlSetText [_idcDownBtn0 + 1, "Eject"]

; false call preprocessFile "Rts\Dialog\InitSilider.sqf"

# Update
	; ctrlShow [IDC_DialogBG_CR, !bool_TZK_DarkSkin]; ctrlShow [IDC_DialogBG_Light, bool_TZK_DarkSkin]
	? call dlgUpdated: goto "Quit"
	? not alive player: closeDialog 0; goto "Quit"

	? btnValue00 >= 0: _btnVal = btnValue00; btnValue00 = -1; goto "LeftBtn"
	? btnValue01 > 0: _btnVal = btnValue01; btnValue01 = 0; goto "DownBtn"
	? btnValue02 > 0: _btnVal = btnValue02; btnValue02 = 0; goto "LeftDownBtn"
	? btnValue03 > 0: _btnVal = btnValue03; btnValue03 = 0; goto "RightDownBtn"

	; Skip
	~0.2
	goto "Update"
# Quit
	[false, _id + 2] exec "Player\Rts\SetRtsMouse.sqs"; exit
# LeftBtn
	? 0 == _btnVal: _pos exec "Player\Rts\sRtsMove.sqs"; closeDialog 0; goto "Quit"
	? 1 == _btnVal: 0 exec "Player\Rts\sRtsClear.sqs"; closeDialog 0; goto "Quit"
	? 2 == _btnVal: [_pos, false, true] exec "Rts\SubMenu\AreaArt.sqs"; closeDialog 0; goto "Quit"
	? 3 == _btnVal: [_pos, true, true] exec "Rts\SubMenu\AreaArt.sqs"; closeDialog 0; goto "Quit"
	? 4 == _btnVal: _pos exec "Rts\SubMenu\BoardPreset.sqs"; closeDialog 0; goto "Quit"
	goto "Update"
# DownBtn
	? 2^0 == _btnVal: 0 exec "Player\Dialog\DisbandPreset.sqs"; closeDialog 0; goto "Quit"
	? 2^1 == _btnVal: 0 exec "Player\Rts\sEject.sqs"; closeDialog 0; goto "Quit"
	goto "Update"
# LeftDownBtn
	if (2^8 == _btnVal) then {_level = _level - 1} else {_level = _level + 1}
	_level call preprocessFile "Player\Rts\RemoveDeadSelectedUnit.sqf"
	[_idcLeftDownBtn0, _level, _btnVal] call preprocessFile "Rts\Dialog\UpdateRtsDlgType.sqf"
	goto "Update"
# RightDownBtn
	? _btnVal > 0: call preprocessFile "Rts\Dialog\SwitchDialogBehavior.sqf"; goto "Quit"
	goto "Update"