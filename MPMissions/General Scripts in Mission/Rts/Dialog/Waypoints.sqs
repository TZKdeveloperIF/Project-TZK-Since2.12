; args: [pos, player group selected units]
_pos = _this select 0, _units = _this select 1

@ !mutexDlg
	mutexDlg = true; dlgID = dlgID + 1; _dlgID = dlgID
	_dlg = createDialog "TzkCmdMapDlgWpCo"

	RtsPriorityPointDialogEnum = 2

mutexDlg = false

@ not mutexRtsMouse
	mutexRtsMouse = true
	_id = rtsMouseId
	mutexRtsMouse = false
[true, _id + 1, _pos] exec "Player\Rts\SetRtsMouse.sqs";

_pos call preprocessFile "Util\DebugPointInfo.sqf"

; init right-down corner buttons (switching to other rts dialogs)
; 0: rts, 1: area, 2: wpco, 3: player order, 4: point
[2, _units] call preprocessFile "Rts\Dialog\InitSwitchDialogBtns.sqf"

_idcBigLeftBtn = IDC + 16 * 13

_idcList = IDC + 16 * 4 + 0

_idcBtn_0_0 = IDC + 16 * 17; _idcBtnBg_0_0 = IDC + 16 * 18;
{ctrlSetText [_x, "co"]} forEach [_idcBtn_0_0]
{ctrlSetText [_x, "wp"]} forEach [_idcBtn_0_0 + 1]
{_i = _x; {ctrlShow [_x + _i, false]} forEach [_idcBtnBg_0_0]} forEach [0,1]

_co = isCommander; _Superior = bIsAiSuperior

_COcount = (countWPCO/2) - (countWPCO/2)%1

{ ctrlShow [_x, _co] } forEach [_idcAction3, _idcAction4]

ctrlSetText [IDC_TITLE, "Waypoints"]
ctrlSetText [_idcBigLeftBtn + 0, "Set"]
ctrlSetText [_idcBigLeftBtn + 1, "Clear"]
ctrlSetText [_idcBigLeftBtn + 2, "Clear All Player WPs"]
ctrlSetText [_idcBigLeftBtn + 3, "Clear All CO WPs"]
ctrlSetText [_idcBigLeftBtn + 4, "Order AI groups"]

_refreshList = preprocessFile "Rts\Dialog\Wp\UpdateList.sqf"

bNewCoSet = false; call _refreshList

btnBigLeft = -1

; read wpco mode from player global variable
_mode = RtsWpCoMode; _cacheMode = _mode; _invalidMode = {not _co && not _Superior && (0 == _mode)}
? not _co && not _Superior && 0 == _mode: _mode = 1

btnValue01 = 2 ^ _mode
_highlight = "CO"; if (1 == _mode) then {_highlight = "WP"}

# Update
	; ctrlShow [IDC_DialogBG_CR, !bool_TZK_DarkSkin]; ctrlShow [IDC_DialogBG_Light, bool_TZK_DarkSkin]

	_co = isCommander; _Superior = bIsAiSuperior
	_biasCo = 0
	? not _co && _Superior: _biasCo = _COcount
	? call _invalidMode: _mode = 1, goto "OnHighlightChange"

	? call dlgUpdated: goto "Quit"
	? !(alive player): closeDialog 0; goto "Quit"

	? btnBigLeft >= 0: _leftBtn = btnBigLeft, btnBigLeft = -1, goto "BigLeftBtn"
	? btnValue01 > 0: _mode = ln btnValue01 / ln 2, btnValue01 = 0, goto "OnHighlightChange"
	? btnValue03 > 0: _btnVal = btnValue03; btnValue03 = 0; goto "RightDownBtn"

	? bNewCoSet: bNewCoSet = false; call _refreshList

	~ 0.2
	goto "Update"

# BigLeftBtn
	? 0 == _leftBtn: goto "SetBtn"
	? 1 == _leftBtn: goto "Clear"
	? 2 == _leftBtn: goto "ClearAllPlayer"
	? 3 == _leftBtn: goto "ClearAllCO"
	? 4 == _leftBtn: goto "OrderAI"

# OnHighlightChange
	; _mode is both the exponent of btnValue01 and the index of indexWayPointSelected

	; do nothing (recover mode from cache) if not commander/superior and trying to change to co/server target
	? call _invalidMode: _mode = _cacheMode; goto "Update"

	_cacheMode = _mode; RtsWpCoMode = _mode
	_bValid = false
	? 0 == _mode: if (_co || _Superior) then {_highlight = "CO", _bValid = true}
	? 1 == _mode: _highlight = "WP", _bValid = true
	? _bValid: call _refreshList, {ctrlShow [_idcBtnBg_0_0 + _x, false]} forEach [0,1], ctrlShow [_idcBtnBg_0_0 + _mode, true]
	goto "Update"

# RightDownBtn
	? _btnVal > 0: call preprocessFile "Rts\Dialog\SwitchDialogBehavior.sqf"; goto "Quit"
	goto "Update"

# SetBtn
	_sel = lbCurSel _idcList
	? _sel == -1: hint "No waypoint selected"; goto "Update"
	indexWayPointSelected set [_mode, _sel]
	_coor = _pos

	? "CO" == _highlight: [siPlayer, giPlayer, _coor, _sel + _biasCo] exec "Common\Msg\sSetWPCO.sqs"
	? "WP" == _highlight: [_sel, _coor] exec "Player\SetWaypoint.sqs"

	closeDialog 0
	goto "Quit"
	
# Clear
	_sel = lbCurSel _idcList
	? _sel == -1: hint "No waypoint selected"; goto "Update"
	indexWayPointSelected set [_mode, _sel]
	_coor = [-1, -1]

	? "CO" == _highlight: [siPlayer, giPlayer, _coor, _sel + _biasCo] exec "Common\Msg\sSetWPCO.sqs"
	? "WP" == _highlight: [_sel, _coor] exec "Player\SetWaypoint.sqs"

	~ 0.2
	goto "Update"

# ClearAllPlayer
	indexWayPointSelected set [1, -1]
	_i = 0; while {_i < countWPPlayer} do {[_i, [-1,-1]] exec "Player\SetWaypoint.sqs", _i = _i + 1}
	call _refreshList
	goto "Update"

# ClearAllCO
	? "CO" == _highlight: [siPlayer, giPlayer, [-1, -1], -1] exec "Common\Msg\sSetWPCO.sqs"
	~ 0.2
	goto "Update"

# OrderAI
	closeDialog 0
	~0.1
	[] exec localize {TZK_DIALOG_AI_GRP_ORDER}
	goto "Quit"

# Quit
	[false, _id + 2] exec "Player\Rts\SetRtsMouse.sqs"; exit