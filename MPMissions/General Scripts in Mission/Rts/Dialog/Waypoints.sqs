; args: [pos, player group selected units]
_pos = _this select 0, _units = _this select 1

@ !mutexDlg
	mutexDlg = true; dlgID = dlgID + 1; _dlgID = dlgID
	_dlg = createDialog "TzkCmdMapDlgWpCo"

	RtsPriorityPointDialogEnum = 2

mutexDlg = false

@ not mutexRtsMouse
	mutexRtsMouse = true
	_id = rtsMouseId
	mutexRtsMouse = false
[true, _id + 1, _pos] exec (TzkScripts select 363);

_pos call preprocessFile "Util\DebugPointInfo.sqf"

; init right-down corner buttons (switching to other rts dialogs)
; 0: rts, 1: area, 2: wpco, 3: player order, 4: point
[2, _units] call preprocessFile "Rts\Dialog\InitSwitchDialogBtns.sqf"

_idcBigLeftBtn = IDC + 16 * 13

_idcList = IDC + 16 * 4 + 0
_idcDevidedList0 = IDC + 16 * 4 + 1
_idcDevidedList1 = IDC + 16 * 4 + 2
{ctrlShow [_x, false]} forEach [_idcDevidedList0, _idcDevidedList1]

_idcBtn_0_0 = IDC + 16 * 17; _idcBtnBg_0_0 = IDC + 16 * 18;
_downBtnBgIdxes = [0,1,3]
{ctrlSetText [_x, "co"]} forEach [_idcBtn_0_0]
{ctrlSetText [_x, "wp"]} forEach [_idcBtn_0_0 + 1]
{ctrlSetText [_x, format ["%1 %2", localize {TZK_LANG_AI}, localize {TZK_LANG_SHORT_ARTILLERY}]]} forEach [_idcBtn_0_0 + 3]
{_i = _x; {ctrlShow [_x + _i, false]} forEach [_idcBtnBg_0_0]} forEach _downBtnBgIdxes
ctrlShow [_idcBtn_0_0 + 3, not bool_TZK_Rts_Cmd_Mode]

_co = isCommander; _Superior = bIsAiSuperior

_COcount = (countWPCO/2) - (countWPCO/2)%1

{ ctrlShow [_x, _co] } forEach [_idcAction3, _idcAction4]

ctrlSetText [IDC_TITLE, localize {TZK_LANG_WAYPOINT}]
ctrlSetText [_idcBigLeftBtn + 0, localize {TZK_LANG_SET}]
ctrlSetText [_idcBigLeftBtn + 1, localize {TZK_LANG_CLEAR}]
ctrlSetText [_idcBigLeftBtn + 2, format ["%1 %2 %3 WPs", localize {TZK_LANG_CLEAR}, localize {TZK_LANG_ALL}, localize {TZK_LANG_PLAYER}]]
ctrlSetText [_idcBigLeftBtn + 3, format ["%1 %2 CO WPs", localize {TZK_LANG_CLEAR}, localize {TZK_LANG_ALL}]]
ctrlSetText [_idcBigLeftBtn + 4, format ["%1 %2 %3", localize {TZK_LANG_ORDER}, localize {TZK_LANG_AI}, localize {TZK_LANG_GROUP}]]

_refreshList = preprocessFile "Rts\Dialog\Wp\UpdateList.sqf"

bNewCoSet = false; call _refreshList

btnBigLeft = -1
btnFirstCtrl = false
ctrlSetText [IDC + 16 * 32 + 2, localize {TZK_LANG_POINT}]
btnFunction = 0

; read wpco mode from player global variable
_mode = RtsWpCoMode; _cacheMode = _mode
_invalidMode = {(not _co && not _Superior && (0 == _mode || 3 == _mode)) || (_mode != 0 && _mode != 1 && bool_TZK_Rts_Cmd_Mode)}
? call _invalidMode: _mode = (if (_co || _Superior) then {0} else {1})

btnValue01 = 2 ^ _mode
_highlight = "CO"; if (1 == _mode) then {_highlight = "WP"}
btnValue03 = 0

; special logic for traditional mode: wp/target
_lastWpIdx = -1; _lastArtIdx = -1
_procMixedWpArt = preprocessFile "Rts\Dialog\Wp\ProcMixedWpArtList.sqf"
; todo: remember last choice

# Update
	; ctrlShow [IDC_DialogBG_CR, !bool_TZK_DarkSkin]; ctrlShow [IDC_DialogBG_Light, bool_TZK_DarkSkin]

	_co = isCommander; _Superior = bIsAiSuperior
	_biasCo = 0
	? not _co && _Superior: _biasCo = _COcount
	? call _invalidMode: _mode = 1, goto "OnHighlightChange"

	? call dlgUpdated: goto "Quit"
	? !(alive player): closeDialog 0; goto "Quit"

	call _procMixedWpArt

	? btnBigLeft >= 0: _leftBtn = btnBigLeft, btnBigLeft = -1, goto "BigLeftBtn"
	? btnValue01 > 0: _mode = ln btnValue01 / ln 2, btnValue01 = 0, goto "OnHighlightChange"
	? btnValue03 > 0: _btnVal = btnValue03; btnValue03 = 0; goto "RightDownBtn"
	? btnFirstCtrl: btnFirstCtrl = false; goto "FirstCtrl"
	? btnFunction > 0: _btnFnc = btnFunction, btnFunction = 0, goto "FunctionalBtns"

	? bNewCoSet: bNewCoSet = false; call _refreshList

	~ 0.2
	goto "Update"

# BigLeftBtn
	? 0 == _leftBtn: goto "SetBtn"
	? 1 == _leftBtn: goto "Clear"
	? 2 == _leftBtn: goto "ClearAllPlayer"
	? 3 == _leftBtn: goto "ClearAllCO"
	? 4 == _leftBtn: goto "OrderAI"

# OnHighlightChange
	; _mode is both the exponent of btnValue01 and the index of indexWayPointSelected

	; do nothing (recover mode from cache) if not commander/superior and trying to change to co/server target
	? call _invalidMode: _mode = _cacheMode; goto "Update"

	_cacheMode = _mode; RtsWpCoMode = _mode
	_bValid = false
	? 0 == _mode: if (_co || _Superior) then {_highlight = "CO", _bValid = true}
	? 1 == _mode: _highlight = "WP", _bValid = true
	? 3 == _mode: _highlight = "SvrTarget", _bValid = true
	? _bValid: call _refreshList, {ctrlShow [_idcBtnBg_0_0 + _x, false]} forEach _downBtnBgIdxes, ctrlShow [_idcBtnBg_0_0 + _mode, true]
	goto "Update"

# RightDownBtn
	? _btnVal > 0: call preprocessFile "Rts\Dialog\SwitchDialogBehavior.sqf"; goto "Quit"
	goto "Update"

# SetBtn
	_done = false
	? "WP" == _highlight && not bool_TZK_Rts_Cmd_Mode: _done = true call preprocessFile "Rts\Dialog\Wp\SetMixedWpArtBtn.sqf"
	? "WP" == _highlight && not bool_TZK_Rts_Cmd_Mode && not _done: hint "No item selected"; goto "Update"
	? _done: closeDialog 0; goto "Quit"

	_sel = lbCurSel _idcList
	? _sel == -1: hint "No waypoint selected"; goto "Update"
	indexWayPointSelected set [_mode, _sel]
	_coor = _pos

	? "CO" == _highlight: [siPlayer, giPlayer, _coor, _sel + _biasCo] exec "Common\Msg\sSetWPCO.sqs"
	? "WP" == _highlight: [_sel, _coor] exec "Player\SetWaypoint.sqs"
	; create a 50x50 art area
	? "SvrTarget" == _highlight: [100 + _sel, "Art", true, "RtsAreaArtSvrIdx", [_coor, [25, 25], 0]] call preprocessFile "Rts\Marker\GenArea.sqf"

	closeDialog 0
	goto "Quit"
	
# Clear
	_done = false
	? "WP" == _highlight && not bool_TZK_Rts_Cmd_Mode: _done = false call preprocessFile "Rts\Dialog\Wp\SetMixedWpArtBtn.sqf"
	? "WP" == _highlight && not bool_TZK_Rts_Cmd_Mode && not _done: hint "No item selected"; goto "Update"
	? _done: closeDialog 0; goto "Quit"

	_sel = lbCurSel _idcList
	? _sel == -1: hint "No waypoint selected"; goto "Update"
	indexWayPointSelected set [_mode, _sel]
	_coor = [-1, -1]

	? "CO" == _highlight: [siPlayer, giPlayer, _coor, _sel + _biasCo] exec "Common\Msg\sSetWPCO.sqs"
	? "WP" == _highlight: [_sel, _coor] exec "Player\SetWaypoint.sqs"
	? "SvrTarget" == _highlight: [_sel, "Art", true, "RtsAreaArtSvrIdx", [_coor, [], 0]] call preprocessFile "Rts\Marker\GenArea.sqf"

	~ 0.2
	goto "Update"

# ClearAllPlayer
	indexWayPointSelected set [1, -1]
	_i = 0; while {_i < countWPPlayer} do {[_i, [-1,-1]] exec "Player\SetWaypoint.sqs", _i = _i + 1}
	call _refreshList
	goto "Update"

# ClearAllCO
	? "CO" == _highlight: [siPlayer, giPlayer, [-1, -1], -1] exec "Common\Msg\sSetWPCO.sqs"
	~ 0.2
	goto "Update"

# OrderAI
	closeDialog 0
	~ 0.1
	0 exec (TzkScripts select 014)
	goto "Quit"

# Quit
	[false, _id + 2] exec (TzkScripts select 363); exit
# FirstCtrl
	; switch to waypoint dialog rapidly
	_btnVal = 2 ^ 4
	call preprocessFile "Rts\Dialog\SwitchDialogBehavior.sqf"; goto "Quit"
# FunctionalBtns
	? 1 == _btnFnc: 0 exec (TzkScripts select 023); closeDialog 0; goto "Quit"
	goto "Update"