; args: none
; this script is familar with "Join\Submenu\PplOrder.sqs"
_top = call preprocessFile (TzkScripts select 371)
_c = count _top
? _c == 0: exit

@ !mutexDlg
mutexDlg = true; dlgID = dlgID + 1; _dlgID = dlgID
_dlg = createDialog "TzkRtsSubMenuOneListBox"
mutexDlg = false

_idcTitle = IDC + 0; _idcListLabel = IDC + 16 * 3 + 1; _idcListBox0 = IDC + 16 * 3 + 0

ctrlSetText [_idcTitle, localize {TZK_LANG_JOIN}]
ctrlSetText [_idcListLabel, format ["selected: %1", _c]]

_groups = groupMatrix select siPlayer
_groupsAI = groupAiMatrix select siPlayer
_groupsName = groupNameMatrix select siPlayer

btnValue00 = false; btnValue01 = false

; short key
	_getShortKeyFunc = preprocessFile "UI\ShortKey\proc.sqf"
	_idcShortKey = IDC + 16 * 1 + 2
	ctrlSetText [_idcShortKey, TzkInGameText select 014]; ctrlShow [_idcShortKey, UsedVersion >= 2030]

# loop
	lbClear _idcListBox0
	_i = 0; _c = count _groups
	# updateList
		_g = _groups select _i
		lbAdd [_idcListBox0, format ["%1 %2 %3", [name leader _g, "AI"] select (_g in _groupsAI), _groupsName select _i, count units _g]]
		_i = _i + 1
		? _i < _c: goto "updateList"
	~ 0.04
	; ctrlShow [IDC_DialogBG_CR, !bool_TZK_DarkSkin]; ctrlShow [IDC_DialogBG_Light, bool_TZK_DarkSkin]
	? call dlgUpdated: goto "Quit"
	? not alive player: closeDialog 0; goto "Quit"

	_shortKeyFunc = "dlg_one_list" call _getShortKeyFunc
	? _shortKeyFunc != "": goto "ProcShortKey"

	? btnFunction > 0: _btnFnc = btnFunction, btnFunction = 0, goto "FunctionalBtns"

	? btnValue00: btnValue00 = false; closeDialog 0; goto "Quit"
	? btnValue01: btnValue01 = false; goto "OK"

	goto "loop"
# Quit
	exit
# ProcShortKey
	? "confirm" == _shortKeyFunc: goto "OK"

	? [_shortKeyFunc, _idcListBox0, 1, 10] call preprocessFile "UI\ShortKey\key2selectOneList.sqf": goto "loop"

	goto "loop"
# FunctionalBtns
	? 3 == _btnFnc: ["dlg_one_list", _this, "Rts\SubMenu\JoinPreset.sqs"] exec "UI\Shortkey\dlg.sqs"; closeDialog 0; goto "Quit"
	goto "loop"
# OK
	_giTo = lbCurSel _idcListBox0
	? -1 == _giTo: hint "No selected group.", goto "loop"

	; join black list check
	_groupTo = _groups select _giTo
	_bInBlackList = not (_groupTo in _groupsAI) && -1 != TzkJoinBlackList find (name leader _groupTo)
	? _bInBlackList: hint format ["%1 is in Join black list", name leader _groupTo], goto "loop"

	; group and unit analysis
	_res = call preprocessFile "Rts\Submenu\Join.sqf"
	_aiUnits = _res select 0, _aiGis = _res select 1, _pplUnits = _res select 2

	? count _aiUnits + count _pplUnits <= 0: hint "No available units to join.", goto "loop"
	? count _aiUnits + count _pplUnits > 11: hint "Selected too many units (no more than 11).", goto "loop"

	[_pplUnits, _giTo, giPlayer] exec "Rts\Trigger\sJoinPpl.sqs"
	[_aiUnits, _aiGis, _giTo, giPlayer] exec "Join\Request.sqs"
	closeDialog 0; goto "Quit"