; args: [clicked pos: [x, y], move before shooting: boolean, server rts order: boolean, player group units: []]

@ !mutexDlg
mutexDlg = true; dlgID = dlgID + 1; _dlgID = dlgID
_dlg = createDialog "TzkRtsArtSubMenu"
mutexDlg = false

_bSvrRtsOrder = _this select 2
_markerType = "Art"
_webShort = (if _bSvrRtsOrder then {"Svr"} else {"Ppl"})
_cachedIdxName = format ["RtsArea%1%2Idx", _markerType, _webShort]

_enum = _markerType call preprocessFile "Rts\Marker\EnumOfType.sqf"
? _enum < 0: showDebug [format ["Invalid %1 marker type.", _markerType], 5000], exit
_cnt = count (TzkPplRtsAreaInfo select _enum)
_updateList = preprocessFile "Rts\SubMenu\UpdateAreaListbox.sqf"

_idcTitle = IDC + 0; _idcBtnLabel0 = IDC + 5; _idcBtnLabel1 = IDC + 6
ctrlSetText [_idcBtnLabel0, "Type"]
ctrlSetText [_idcBtnLabel1, "Big Angle"]
_idcBtn_0_0 = IDC + 16 * 2; _idcBtnBg_0_0 = IDC + 16 * 1;
_idcBtn_1_0 = IDC + 16 * 18; _idcBtnBg_1_0 = IDC + 16 * 17;
ctrlSetText [_idcTitle, "Shoot Area"]
{ctrlSetText [_x, "Soldier"]} forEach [_idcBtn_0_0, _idcBtn_1_0]
{ctrlSetText [_x, "Struct"]} forEach [_idcBtn_0_0 + 1, _idcBtn_1_0 + 1]
{ctrlSetText [_x, "Howitzer"]} forEach [_idcBtn_0_0 + 2, _idcBtn_1_0 + 2]
{ctrlSetText [_x, "MLRS"]} forEach [_idcBtn_0_0 + 3, _idcBtn_1_0 + 3]
{ctrlSetText [_x, "Tank"]} forEach [_idcBtn_0_0 + 4]
{_i = _x; {ctrlShow [_x + _i, false]} forEach [_idcBtnBg_0_0, _idcBtnBg_1_0]} forEach [0,1,2,3,4]

_idcMlrsShootingCnt = IDC + 16 * 0 + 7

_idcListLabel0 = IDC + 1; _idcListBox0 = IDC + 2; _idcListLabel1 = IDC + 3; _idcListBox1 = IDC + 4
ctrlSetText [_idcListLabel0, "Speed"]
ctrlSetText [_idcListLabel1, "Art Area"]
[_idcListBox1, _markerType, _webShort, _cnt] call _updateList
? -1 != (call _cachedIdxName): lbSetCurSel [_idcListBox1, (call _cachedIdxName)]

_i = lbAdd [_idcListBox0, "Do nothing"]; lbSetValue [_idcListBox0, _i, -1]
_i = lbAdd [_idcListBox0, "80"]; lbSetValue [_idcListBox0, _i, 80]
_i = lbAdd [_idcListBox0, "120"]; lbSetValue [_idcListBox0, _i, 120]
_i = lbAdd [_idcListBox0, "180"]; lbSetValue [_idcListBox0, _i, 180]
_i = lbAdd [_idcListBox0, "300"]; lbSetValue [_idcListBox0, _i, 300]
_i = lbAdd [_idcListBox0, "400"]; lbSetValue [_idcListBox0, _i, 400]
_i = lbAdd [_idcListBox0, "500"]; lbSetValue [_idcListBox0, _i, 500]
; _i = lbAdd [_idcListBox0, "Auto"]; lbSetValue [_idcListBox0, _i, -2]
lbSetCurSel [_idcListBox0, 0]

btnValue00 = false; btnValue01 = 0
btnValue02 = [true,true,true,true,true]; btnValue03 = [false,false,false,false]

# loop
	~ 0.2
	; ctrlShow [IDC_DialogBG_CR, !bool_TZK_DarkSkin]; ctrlShow [IDC_DialogBG_Light, bool_TZK_DarkSkin]
	? call dlgUpdated: goto "Quit"
	? not alive player: closeDialog 0; goto "Quit"

	call preprocessFile "Rts\SubMenu\UpdateShootAreaBtn.sqf"
	_text = ctrlText _idcMlrsShootingCnt
	? sizeofstr _text > 2: ctrlSetText [_idcMlrsShootingCnt, substr [_text, 0, 2]], hint "Don't input too large number."

	? btnValue00: btnValue00 = false; closeDialog 0; goto "Quit"
	? btnValue01 > 0: _btnVal = btnValue01, btnValue01 = 0; goto "Execution"

	goto "loop"
# Quit
	btnValue02 = 0; btnValue03 = 0
	exit
# Execution
	? -1 == lbCurSel _idcListBox0: hint "No speed selected."; goto "loop"
	? -1 == lbCurSel _idcListBox1: hint "No art area selected"; goto "loop"
	? -10 == getMarkerPos (format ["RtsArea%1%2%3", _markerType, _webShort, lbCurSel _idcListBox1]) select 0: hint "Area undefined."; goto "loop"

	_orderType = "General Area"
	? 2 == _btnVal: _orderType = "Destroy Obstruction"

	_mlrsCnt = [ctrlText _idcMlrsShootingCnt, 0] call preprocessFile "Util\Str2Num.sqf";

	[+btnValue02, +btnValue03, lbValue [_idcListBox0, lbCurSel _idcListBox0], lbCurSel _idcListBox1, _this, _orderType, _mlrsCnt] exec "Rts\Order\sRtsShootArea.sqs"
	closeDialog 0; goto "Quit"