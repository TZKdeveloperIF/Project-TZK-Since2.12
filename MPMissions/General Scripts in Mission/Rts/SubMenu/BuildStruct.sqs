; args: pos
_pos = _this

@ !mutexDlg
mutexDlg = true; dlgID = dlgID + 1; _dlgID = dlgID
_dlg = createDialog "TzkRtsSubMenuTwoListBox"
mutexDlg = false

_idcTitle = IDC + 0
_idcListLabel0 = IDC + 16 * 3 + 1; _idcListLabel1 = IDC + 16 * 3 + 3
_idcListBox0 = IDC + 16 * 3 + 0; _idcListBox1 = IDC + 16 * 3 + 2

ctrlSetText [_idcTitle, "Build Struct"]

_param0 = ["Structure Type", count structDefs, {format[{%1}, structDefs select _this select sdName]}]
_param1 = ["Angle",24,{format [{%1%2}, 15*(_this), [[" (N)", " (NE)", " (E)", " (SE)", " (S)", " (SW)", " (W)", " (NW)"] select ((_this - (_this mod 3))/3),""] select (_this mod 3 != 0)]}];

ctrlSetText [_idcListLabel0, _param0 select 0]; lbClear _idcListBox0; _i=0; while "_i < (_param0 select 1)" do { lbAdd [_idcListBox0, _i call (_param0 select 2)]; _i=_i+1 }; lbSetCurSel [_idcListBox0, RtsAreaBuildStruct select 0]
ctrlSetText [_idcListLabel1, _param1 select 0]; lbClear _idcListBox1; _i=0; while "_i < (_param1 select 1)" do { lbAdd [_idcListBox1, _i call (_param1 select 2)]; _i=_i+1 }; lbSetCurSel [_idcListBox1, RtsAreaBuildStruct select 1]

btnValue00 = false; btnValue01 = false; btnValue02 = false

# loop
	~ 0.2
	; ctrlShow [IDC_DialogBG_CR, !bool_TZK_DarkSkin]; ctrlShow [IDC_DialogBG_Light, bool_TZK_DarkSkin]
	? call dlgUpdated: goto "Quit"
	? not alive player: closeDialog 0; goto "Quit"

	? btnValue00: btnValue00 = false; closeDialog 0; goto "Quit"
	? btnValue01: btnValue01 = false; goto "OK"
	? btnValue02: btnValue02 = false; goto "Undo"

	goto "loop"
# Quit
	exit
# OK
	RtsAreaBuildStruct set [0, lbCurSel _idcListBox0]; RtsAreaBuildStruct set [1, lbCurSel _idcListBox1];
	[_pos, lbCurSel _idcListBox0, lbCurSel _idcListBox1] exec "Player\Order\BuildStructure.sqs"
	closeDialog 0; goto "Quit"
# Undo
	0 exec localize {TZK_FUNC_PLAYER_UNDO_BUILD}
	goto "loop"