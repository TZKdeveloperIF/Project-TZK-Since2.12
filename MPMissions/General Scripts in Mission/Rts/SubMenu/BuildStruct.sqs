; args: pos
_pos = _this

@ !mutexDlg
mutexDlg = true; dlgID = dlgID + 1; _dlgID = dlgID
_dlg = createDialog "TzkRtsBuildStructSubMenu"
mutexDlg = false

@ not mutexRtsMouse
	mutexRtsMouse = true
	_id = rtsMouseId
	mutexRtsMouse = false
[true, _id + 1, _pos] exec "Player\Rts\SetRtsMouse.sqs";

_idcTitle = IDC + 0
_idcListLabel0 = IDC + 16 * 3 + 1; _idcListLabel1 = IDC + 16 * 3 + 3
_idcListBox0 = IDC + 16 * 3 + 0; _idcListBox1 = IDC + 16 * 3 + 2
_idcListLabel2 = IDC + 16 * 3 + 5; _idcListBox2 = IDC + 16 * 3 + 4

ctrlSetText [_idcTitle, "Build Struct"]
ctrlSetText [_idcListLabel0, "Structure Type"]
; direction
ctrlSetText [_idcListLabel1, "Direction"]
_texts = ["0 (N)", "45 (NE)", "90 (E)", "135 (SE)", "180 (S)", "225 (SW)", "270 (W)", "315 (NW)"]
_i = 0; while {_i < count _texts} do {lbAdd [_idcListBox1, _texts select _i]; _i = _i + 1}
; correction
ctrlSetText [_idcListLabel2, "Correction"]
_texts = ["-30", "-15", "0", "+15", "+30"]
_i = 0; while {_i < count _texts} do {lbAdd [_idcListBox2, _texts select _i]; _i = _i + 1}

; highlight buttons
_idcBtn = IDC + 16 * 5; _idcBgBtn = IDC + 16 * 6
_texts = ["Factory","Building","Defend","Art","Walls","Obstruct"]
_i = 0; while {_i < count _texts} do {ctrlSetText [_idcBtn + _i, _texts select _i], _i = _i + 1}
_i = 0; while {_i < count _texts} do {ctrlShow [_idcBgBtn + _i, false], _i = _i + 1}

; load from config
; elements: 0: highlight structure type, 1: index of structure list, 2: index of direction, 3: index of correction
_habit = "RtsBuildStructCache" call preprocessFile "Cfg\LoadPlayerValue.sqf";
; disable "factory" for non-commander
ctrlEnable [_idcBtn + 0, isCommander]
? not isCommander && _habit select 0 == 0: _habit set [0, 1]
; initialize structure list
btnValue00 = -1; btnValue02 = _habit select 0; _lastHighlightEnum = btnValue02
_updateStructList = preprocessFile "Rts\Submenu\UpdateBuildStructList.sqf"
[_lastHighlightEnum, _idcListBox0] call _updateStructList
; initial current selected item
lbSetCurSel [_idcListBox0, _habit select 1]
lbSetCurSel [_idcListBox1, _habit select 2]
lbSetCurSel [_idcListBox2, _habit select 3]

# loop
	~ 0.2
	; ctrlShow [IDC_DialogBG_CR, !bool_TZK_DarkSkin]; ctrlShow [IDC_DialogBG_Light, bool_TZK_DarkSkin]
	? call dlgUpdated: goto "Quit"
	? not alive player: closeDialog 0; goto "Quit"

	? btnValue00 >= 0: goto "BtnVal0"
	? _lastHighlightEnum != btnValue02: goto "OnHightlightEnumChanged"

	goto "loop"
# BtnVal0
	_val = btnValue00; btnValue00 = -1
	? 0 == _val: closeDialog 0; goto "Quit"
	? 1 == _val: goto "OK"
	? 2 == _val: goto "Undo"
	goto "loop"
# Quit
	[false, _id + 2] exec "Player\Rts\SetRtsMouse.sqs"
	exit
# OK
	_idx0 = lbCurSel _idcListBox0; _idx1 = lbCurSel _idcListBox1; _idx2 = lbCurSel _idcListBox2
	? -1 == _idx0: hint "No structure selected."; goto "loop"
	; save to config
	["RtsBuildStructCache", [_lastHighlightEnum, _idx0, _idx1, _idx2]] call preprocessFile "Cfg\SavePlayerValue.sqf"

	[_pos, lbValue [_idcListBox0, lbCurSel _idcListBox0], _idx1 * 45 + _idx2 * 15 - 30] exec "Player\Order\BuildStructure.sqs"
	closeDialog 0; goto "Quit"
# Undo
	0 exec (TzkScripts select 135)
	goto "loop"
# OnHightlightEnumChanged
	_lastHighlightEnum = btnValue02
	; update structure list
	[_lastHighlightEnum, _idcListBox0] call _updateStructList
	goto "loop"