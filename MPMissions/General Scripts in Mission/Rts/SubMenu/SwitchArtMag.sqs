; args: [server rts order: bool, params]
_bSvrRtsOrder = _this select 0
? not _bSvrRtsOrder: _units = _this select 1, if (count _units <= 0) then {exit}

@ !mutexDlg
mutexDlg = true; dlgID = dlgID + 1; _dlgID = dlgID
_dlg = createDialog "TzkRtsSubMenuTwoListBox"
mutexDlg = false

_idcTitle = IDC + 0
_idcListLabel0 = IDC + 16 * 3 + 1; _idcListLabel1 = IDC + 16 * 3 + 3
_idcListBox0 = IDC + 16 * 3 + 0; _idcListBox1 = IDC + 16 * 3 + 2

ctrlSetText [_idcTitle, TzkInGameText select 030 select 1]

_defSpdIdx = 2; if (upgMatrix select siPlayer select upgLongWeaponRange == 2) then {_defSpdIdx = 4}
_param0 = ["InitSpeed", count ArtilleryMagazineSpeedList, {format [{%1 m/s}, ArtilleryMagazineSpeedList select _this]}, _defSpdIdx];
_param1 = ["Curved", 2, {["False", "True"] select _this}, 1];

ctrlSetText [_idcListLabel0, _param0 select 0]; lbClear _idcListBox0 ; _i = 0
while {_i < (_param0 select 1)} do {lbAdd [_idcListBox0 , _i call (_param0 select 2)]; _i = _i + 1}
lbSetCurSel [_idcListBox0, _param0 select 3]
ctrlSetText [_idcListLabel1, _param1 select 0]; lbClear _idcListBox1; _i = 0
while {_i < (_param1 select 1)} do {lbAdd [_idcListBox1, _i call (_param1 select 2)]; _i = _i + 1}
lbSetCurSel [_idcListBox1, _param1 select 3]

btnValue00 = false; btnValue01 = false; btnValue02 = false

; short key
	_getShortKeyFunc = preprocessFile "UI\ShortKey\proc.sqf"
	_idcShortKey = IDC + 16 * 1 + 2
	ctrlSetText [_idcShortKey, TzkInGameText select 014]; ctrlShow [_idcShortKey, UsedVersion >= 2030]

# loop
	~ 0.02
	; ctrlShow [IDC_DialogBG_CR, !bool_TZK_DarkSkin]; ctrlShow [IDC_DialogBG_Light, bool_TZK_DarkSkin]
	? call dlgUpdated: goto "Quit"
	? not alive player: closeDialog 0; goto "Quit"

	_shortKeyFunc = "dlg_one_list" call _getShortKeyFunc
	? _shortKeyFunc != "": goto "ProcShortKey"

	? btnValue00: btnValue00 = false; closeDialog 0; goto "Quit"
	? btnValue01: btnValue01 = false; goto "OK"
	? btnFunction > 0: _btnFnc = btnFunction, btnFunction = 0, goto "FunctionalBtns"

	goto "loop"
# Quit
	exit
# ProcShortKey
	? "confirm" == _shortKeyFunc: goto "OK"

	? [_shortKeyFunc, _idcListBox0, 1, 10] call preprocessFile "UI\ShortKey\key2selectOneList.sqf": goto "loop"

	goto "loop"
# FunctionalBtns
	? 3 == _btnFnc: ["dlg_one_list", _this, "Rts\SubMenu\SwitchArtMag.sqs"] exec "UI\Shortkey\dlg.sqs"; closeDialog 0; goto "Quit"
	goto "loop"
# OK
	_idx0 = lbCurSel _idcListBox0 ; _idx1 = lbCurSel (_idcListBox1)
	? -1 == _idx0 || -1 == _idx1: hint "Params unselected.", goto "loop"

	? _bSvrRtsOrder: [_idx0, _idx1] exec "Rts\Trigger\sArtMag.sqs"
	? not _bSvrRtsOrder: {[_x, _idx0, _idx1] exec "Art\SwitchMagGeneral.sqs"} forEach _units
	closeDialog 0; goto "Quit"