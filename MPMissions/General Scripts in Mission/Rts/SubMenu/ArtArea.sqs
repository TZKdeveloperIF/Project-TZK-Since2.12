; args: script type (enum. 0 for server, 1 for player)

@ !mutexDlg
mutexDlg = true; dlgID = dlgID + 1; _dlgID = dlgID
_dlg = createDialog "TzkRtsSubMenuOneListWide"
mutexDlg = false

_size = getMarkerSize "TzkRtsArea"

_idcTitle = IDC + 0; _idcListLabel = IDC + 16 * 1 + 0; _idcListBox0 = IDC + 16 * 2 + 0

_dispName = "Server"; if (1 == _this) then {_dispName = "Player"}
_markerPrefix = "ArtSvrArea"; if (1 == _this) then {_markerPrefix = "ArtPplArea"}
_msgScript = "Player\Rts\sSetSvrArtArea.sqs"; if (1 == _this) then {_msgScript = "Player\Rts\SetPlayerArtArea.sqs"}
_cachedIdx = RtsSvrArtAreaIdx; if (1 == _this) then {_cachedIdx = RtsPplArtAreaIdx}

ctrlSetText [_idcTitle, format ["Set %1 Art Area", _dispName]]
ctrlSetText [_idcListLabel, "Areas"]

[_idcListBox0, _markerPrefix] call preprocessFile "Rts\SubMenu\UpdateArtAreaDialogInfo.sqf"
? -1 != _cachedIdx: lbSetCurSel [_idcListBox0, _cachedIdx]

btnValue00 = false; btnValue01 = false; btnValue02 = false

? _size select 0 > 200 || _size select 1 > 200: hint "Marker too large. Can't be set as art area."

# loop
	~ 0.2
	; ctrlShow [IDC_DialogBG_CR, !bool_TZK_DarkSkin]; ctrlShow [IDC_DialogBG_Light, bool_TZK_DarkSkin]
	? call dlgUpdated: goto "Quit"
	? not alive player: closeDialog 0; goto "Quit"

	[_idcListBox0, _markerPrefix] call preprocessFile "Rts\SubMenu\UpdateArtAreaDialogInfo.sqf"

	? btnValue00: btnValue00 = false; closeDialog 0; goto "Quit"
	? btnValue01: btnValue01 = false; goto "Set"
	? btnValue02: btnValue02 = false; goto "Clear"

	goto "loop"
# Quit
	exit
# Set
	? -1 == lbCurSel _idcListBox0: hint "No area selected."; goto "loop"
	? _size select 0 > 200 || _size select 1 > 200: hint "Marker too large. Can't be set as art area."; goto "loop"
	(100 + lbCurSel _idcListBox0) exec _msgScript; closeDialog 0; goto "Quit"
# Clear
	? -1 == lbCurSel _idcListBox0: hint "No area selected."; goto "loop"
	(lbCurSel _idcListBox0) exec _msgScript; goto "loop"