; this script is for distributing rts order

; client call another distributor script
? not isServer: _this exec "Rts\Framework\Distribute4Player.sqs"; exit
; non-dedicated server deep copy the parameter to call another distributor script
? not isNull player: _copy = +_this; _copy set [0, +(_this select 0)]; _copy exec "Rts\Framework\Distribute4Player.sqs"

; args: [Network IDs[], param: any, script name: string, script folder: string]

_netIds = _this select 0
_isntAiUnit = preprocessFile "Util\IsNotAiGrpNetId.sqf"
[_netIds, _isntAiUnit] call preprocessFile "Algo\arrayEraseIf.sqf"
_c = count _netIds
? 0 == _c: exit

_scriptFolder = _this select 3
? "Order" == _scriptFolder: goto "Order"
? "Framework" == _scriptFolder: goto "Framework"
? "Trigger" == _scriptFolder: goto "Trigger"

("Unexpected rts script folder for " + (_this select 2)) call fDebugLog
exit

# Order
	@ not mutexTzkRtsW
		mutexTzkRtsW = true

		_this call preprocessFile "Rts\Framework\OrderCtor.sqf"

		mutexTzkRtsW = false
	exit
# Framework
	_scriptName = _this select 2
	_scriptPath = format ["%1\%2\%3", "Rts", "Framework", _scriptName];
	_this exec _scriptPath
	exit
# Trigger
	_scriptName = _this select 2
	_scriptPath = format ["%1\%2\%3", "Rts", "Trigger", _scriptName];
	_this exec _scriptPath
	exit