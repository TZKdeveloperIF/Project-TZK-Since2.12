; args: [vehicle, type, si, gi]
_vehicle = _this select 0; _type = _this select 1; _si = _this select 2; _gi = _this select 3
_desc = unitDefs select _type; _typeName = typeOf _vehicle
; Place stable design here.

; SERVER AND PLAYER
clearMagazineCargo _vehicle; clearWeaponCargo _vehicle

; Killed and Hit Event
_vehicle addEventHandler ["killed", {_this exec "Common\Event_VehicleDestroyed.sqs"}]
[_vehicle, _si, _desc select udCost] exec "Common\EventAdd_VehicleHit.sqs"

; Fired Event
? _typeName in ["HMMMV_TOW2A_xj400", "BRDM2_MNF_xj400", "GunBoatW_Custom_xj400", "GunBoatE_Custom_xj400"]: _vehicle addEventHandler ["Fired", "_this call loadFile {Player\EH\FiredImitated.sqf}" ]
? "Plane" countType [_vehicle] > 0 : _vehicle addEventHandler ["Fired", { _this exec "Player\EH\FiredPlaneMissile.sqs"} ]

; GetOut Event
? "Air" countType [_vehicle] > 0: _vehicle addEventHandler ["GetOut", { _this exec "Common\EH\GetOutAircraft.sqs" }]

; "IncomingMissile" EH must be added to all clients. Although it is a global EH, it should be added to the client who fires missile. The command can be empty, but EH can't be omitted.
? "Air" countType [_vehicle] > 0 : [_vehicle, _type] exec "Common\EH\AddAirIncoming.sqs"
? "Tank" countType [_vehicle] > 0: if (_si != siRes) then {[_vehicle, _type] exec "Common\EH\AddTankIncoming.sqs"}

? isNull player : goto "SkipClient"
; PLAYER ONLY

[_vehicle, _type, _si] call funcInitVehicleMarker
? _typeName in [{A10_Tomahawk_xj400}, {Su25_Raduga_xj400}] : _vehicle say {NuclearMissileReady_xj400}

; _vehicle addEventHandler ["Fired", {[_this, LimitWeaponRangeFactor] call <function>}]
? !(_type in vUnlimitedRange) && !(_type in vDoubledRange) : _vehicle addEventHandler ["Fired",{[_this, 1] call funcFiredEffect}]
? (_type in vDoubledRange) : _vehicle addEventHandler ["Fired",{[_this, 2] call funcFiredEffect}]
? (_type in vUnlimitedRange) : _vehicle addEventHandler ["Fired",{[_this, 0] call funcFiredEffect}]

? "Tank" countType [_vehicle] > 0: [_vehicle, _type] exec "Player\EH\AddTankHit.sqs"

~0.3
? _si == siPlayer && _gi == giPlayer : if (count (crew _vehicle) == 0 && time > 30) then { _text = format ["Your empty %1 has arrived.", (unitDefs select _type) select udName ]; hint _text; player groupchat _text }; if (_type in (airTransport select _si)) then { _vehicle exec "Player\InitTransportHeli.sqs" }

#SkipClient

? !isServer : exit

; SERVER ONLY
? _type in (typesHeavyTank + typesLightTank + typesHowitzer + typesATAPC + typesAAAPC + typesGunshipOrigin + typesGunshipAT) : [_vehicle, _si, _gi] exec "Server\EventAdd_Vehicle.sqs"