; arg: [unit, si, gi]

_unit = _this select 0
_si = _this select 1
_gi = _this select 2

; player globalchat format["AICO_CheckSupport entered, %1", _si]

call busySet

#CheckSupport
_objRepair = objNull

; check structures
_list = repairableStructureMatrix select _si
? (count _list) == 0 : goto "CheckVehicles"
_objRepair = (_list select ((random count _list) - 0.5)) select 0
goto "FindSupport"

#CheckVehicles
_units = (units group _unit) - [_unit]
_i = 0
_c = count _units
#CheckUnit
  ? _i >= _c : goto "Exit"
  _u = _units select _i
  _v = vehicle _u
  _i = _i + 1
  
  ? _u == _v || _u != gunner _v || !(isNull driver _v) : goto "CheckUnit"
  _weaponsNoAmmo = "(_v ammo _x) == 0" count (weapons _v)
  ? _weaponsNoAmmo > 0 || (damage _v) > 0 : _objRepair = _v; goto "FindSupport"
  goto "CheckUnit"

#FindSupport
; player globalchat format["AICO_CheckSupport, _objRepair = %1", _objRepair]
? _unit != vehicle _unit : _support = vehicle _unit; goto "RepairMove"
_support = objNull
_res = [ getPos _unit, 300, [(typesSupport select _si) select 0], [] ] call funcGetNearbyVehicles
_list = []
{ if ( isNull driver (_x select 0) ) then { _list set [count _list, (_x select 0)] } } foreach _res
? (count _list) == 0 : goto "Exit"
_support = _list select ((random count _list) - 0.5)

; player globalchat format["AICO_CheckSupport, _support = %1", _support]

_unit assignAsDriver _support
[_unit] orderGetIn true
_timeAbort = time + 2*60
#WaitInAsDriver

	~5

  ? !(alive _unit) : goto "Exit"
  [_unit, _si, _gi] exec "Server\AICO_CheckBuyUnit.sqs"
  ? !(alive _support) || isNull _objRepair || time > _timeAbort : goto "CheckSupport"
  ? _unit != driver _support : goto "WaitInAsDriver"
  
#RepairMove
_unit doMove ([getPos _objRepair, rangeSupport - 5, rangeSupport - 5] call funcGetRandomPos)
_timeAbort = time + 2*60
#CheckObjectReached

	~5
	
  ? !(alive _unit) : goto "Exit"
  [_unit, _si, _gi] exec "Server\AICO_CheckBuyUnit.sqs"
  ? _unit != (driver _support) || isNull _objRepair || time > _timeAbort : goto "CheckSupport"
  ? (_support distance _objRepair) > rangeSupport : goto "CheckObjectReached"

#RepairObjectReached
_gunner = gunner _objRepair
? !(_gunner in (units group _unit)) : goto "RepairWait"

_heal = false
_rearm = false
? (damage _objRepair) > 0 : _heal = true
{ if ((damage _x) > 0) then { _heal = true } } foreach (crew _objRepair)
_type = _objRepair call funcGetUnitTypeFromObject
_rearmData = _objRepair call funcGetRearmData
_rearmMags = _rearmData select 1
? (count _rearmMags) == 0 : _rearm = false; goto "SkipRearm"
_weapons = weapons _objRepair
{ if ((_objRepair ammo _x) == 0) then { _rearm = true }  } foreach _weapons
#SkipRearm
? _heal : [_unit, _si, _gi, _objRepair, _type, _support] exec "Server\AI_Heal.sqs"
? _rearm : [_unit, _si, _gi, _objRepair, _type, _support] exec "Server\AI_Rearm.sqs"

#RepairWait

	~30
	
? !(alive _unit) : goto "Exit"
[_unit, _si, _gi] exec "Server\AICO_CheckBuyUnit.sqs"
goto "CheckSupport"

#MoveToEjectPos
_unit doMove ([getPos (mhq select _si), 100, 100] call funcGetRandomPos)

	~30
	
? !(alive _unit) : goto "Exit"
unassignVehicle _unit

	~10
	
? !(alive _unit) : goto "Exit"

#Exit
? _unit != vehicle _unit : goto "MoveToEjectPos"
unassignVehicle _unit
call busyClear
; player globalchat format["AICO_CheckSupport left, %1", _si]
exit