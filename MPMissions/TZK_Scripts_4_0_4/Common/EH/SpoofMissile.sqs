; args: [target, ammo, whoFired]
_target = _this select 0; _ammo = _this select 1; _shooter = _this select 2
_missile = nearestobject [_shooter, _ammo]
? !local _target && !local _missile : goto "Quit"
? "_x=={SpoofSwitch_xj400}" count magazines _target > 0 : goto "Quit"

; this script can be applied directly in 2.01 only, since it uses some 2.01 commands.
; it's possible to transplant them into addons or other missions but this remain to those editors

? !alive _target: goto "Quit"
; ? !isengineon _target: goto "Quit"

; Can't spoof if out of SpoofMags
? "_x=={SpoofCount_xj400}" count magazines _target == 0 : goto "Quit"

; Wait till the missile is close
_timeAbort = time + 10
@ (_missile distance _target < 400 || _timeAbort < time)
; can't spoof missile fired very close
? _timeAbort < time || _missile distance _target < 250 : goto "Quit"

; Check if Spoof chance left
? "_x=={SpoofCount_xj400}" count magazines _target == 0 : goto "Quit"

; Mag cost and deflect missile
? local _target : _target removeMagazine "SpoofCount_xj400"
? !isNull player: if ( (driver _target) in units group player ) then {(driver _target) groupChat format ["%1 Spoof Chance left", "_x=={SpoofCount_xj400}" count magazines _target]}; if (player == driver _target) then {hint format ["%1 Spoof Chance left", "_x=={SpoofCount_xj400}" count magazines _target]};

? !local _missile : goto "Quit"

_dirchange = 1.57 + 1.57*(speed _target)/200; _vel = velocity _missile
_newvel = [_vel, _dirchange] call funcRandomRotation3D

; 2.01 command "setVectorDirAndUp"
? !bool_TZK_199_Mode: _missile setVectorDirAndUp [_newvel,[0,0,1]]
_missile setVelocity _newvel

#Quit
exit

; EOF