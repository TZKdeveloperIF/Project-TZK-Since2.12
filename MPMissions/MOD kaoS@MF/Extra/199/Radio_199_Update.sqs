; args: [vehicle player]
_vehicle = _this select 0
{_x setRadioMsg " "} forEach [1,3,4,5,6,7,8,9,10]; 2 setRadioMsg "TZK Mission Setting"

? _vehicle == player: 3 setRadioMsg "Self-Medic", 4 setRadioMsg "MHQ Build Menu", 5 setRadioMsg "Sup Build Menu", 6 setRadioMsg "Read Hint of Radio", 7 setRadioMsg " ", 8 setRadioMsg "Rearm AA", 9 setRadioMsg "Rearm AT", 10 setRadioMsg "Rearm RPG", exit

; Classifications below are defined in InitUnitTypes
#CheckVehicle
_vt = _vehicle call funcGetUnitTypeFromObject
? _vt == -1: goto "CheckStructure"
? _vt in RadioTypesCustomCarShips: hint "This unit shouldn't appear in 1.99 mission. Please report bug to the author.", exit

? _vt in typesHowitzer: hint "This unit shouldn't appear in 1.99 mission. Please report bug to the author.", exit
? _vt in typesRocketLauncher: hint "This unit shouldn't appear in 1.99 mission. Please report bug to the author.", exit

_booleStringTransport = "_vehicle in TransportActivatedVehicles";

? _vt in RadioTypesShips: 3 setRadioMsg "To driver's seat", 4 setRadioMsg "To gunner's seat", 5 setRadioMsg "Attach/Detach Center Vehicle", 6 setRadioMsg "Attach/Detach Right Vehicle", 7 setRadioMsg "Attach/Detach Left Vehicle", 8 setRadioMsg "Eject All Cargo", 9 setRadioMsg "Eject One Cargo", 10 setRadioMsg (if (call _booleStringTransport) Then {"Transport Turn OFF"} Else {"Transport Turn ON"}), exit

; Helis all can attach and support Heli is special in drop ammo crate. Support Plane is more a transport plane. Support Ship nothing to specialize.
? _vt in (RadioTypesSupport + [utMCVW, utMCVE, utMHQ0, utMHQ1]): if ("Land" countType [_vehicle] > 0) then {5 setRadioMsg "Attach/Detach Center Vehicle", 8 setRadioMsg "Eject All Cargo", 9 setRadioMsg "Eject One Cargo", 10 setRadioMsg (if (call _booleStringTransport) Then {"Transport Turn OFF"} Else {"Transport Turn ON"}), exit}

_booleStringSpoofON = "bSpoofMissile && ""_x=={SpoofSwitch_xj400}"" count magazines _vehicle > 0"; _booleSpoofON = call _booleStringSpoofON
_booleStringSpoofOFF = """_x=={SpoofSwitch_xj400}"" count magazines _vehicle == 0 && count magazines _vehicle > 0"; _booleSpoofOFF = call _booleStringSpoofOFF

; Transport Helis and Planes are similar. Support Heli is special in drop ammo crate.
? _vt in RadioTypesTransportPlane + RadioTypesTransportHeli: if _booleSpoofON Then {1 setRadioMsg "SpoofMissile ON"}, if _booleSpoofOFF Then {1 setRadioMsg "SpoofMissile OFF"}, 3 setRadioMsg "To driver's seat", 4 setRadioMsg "To gunner's seat", 5 setRadioMsg "Attach/Detach Center Vehicle", if ("Plane" countType [_vehicle] == 0 && (_vehicle call funcGetTranspVol) > 8) Then {5 setRadioMsg "Attach/Detach Center Vehicle"}, 7 setRadioMsg "Eject AI Units", 8 setRadioMsg "Eject All Cargo", 9 setRadioMsg "Eject One Cargo", 10 setRadioMsg (if (call _booleStringTransport) Then {"Transport Turn OFF"} Else {"Transport Turn ON"}), exit

; Check vehicle's "transportSoldier" to decide whether is a transport Heli or not
? _vt in RadioTypesGunships: if _booleSpoofON Then {1 setRadioMsg "SpoofMissile ON"}, if _booleSpoofOFF Then {1 setRadioMsg "SpoofMissile OFF"}, 3 setRadioMsg "To driver's seat", 4 setRadioMsg "To gunner's seat", if ((_vehicle call funcGetTranspVol) > 0) Then {7 setRadioMsg "Eject AI Units", 8 setRadioMsg "Eject All Cargo", 9 setRadioMsg "Eject One Cargo", 10 setRadioMsg (if (call _booleStringTransport) Then {"Transport Turn OFF"} Else {"Transport Turn ON"})}, exit

_booleAfterBurner = ("A10_Base_xj400" countType [_vehicle] > 0) || ("Su25_Base_xj400" countType [_vehicle] > 0)
? _booleAfterBurner : _booleStringAfterBurnerON = "bAfterBurner && ""_x=={AfterBurnerSwitch_xj400}"" count magazines _vehicle > 0"; _booleABON = call _booleStringAfterBurnerON
? _booleAfterBurner : _booleStringAfterBurnerOFF = " ""_x=={AfterBurnerSwitch_xj400}"" count magazines _vehicle == 0 && count magazines _vehicle > 0"; _booleABOFF = call _booleStringAfterBurnerOFF
; Plane with AfterBurner in TZK is only allowed to be A10/Su25
? _vt in RadioTypesPlane: if _booleSpoofON Then {1 setRadioMsg "SpoofMissile ON"}, if _booleSpoofOFF Then {1 setRadioMsg "SpoofMissile OFF"}, if (_booleABON) then {10 setRadioMsg "AfterBurner ON"}, if (_booleABOFF) then {10 setRadioMsg "AfterBurner OFF"}, exit

; Transport Land Vehicles. Ship will enter script above thus won't reach here.
? _vt in (typesTransportCar + typesTransportAPC): 8 setRadioMsg "Eject All Cargo", 9 setRadioMsg "Eject One Cargo", 10 setRadioMsg (if (call _booleStringTransport) Then {"Transport Turn OFF"} Else {"Transport Turn ON"}), exit

; Vehicle doesn't have special Actions
10 setRadioMsg "Refresh Radio List"; exit


#CheckStructure
_vt = _vehicle call funcGetStructTypeFromObject
? _vt == -1: goto "Abnormal"
? _vt in (structsOccupiableHowitzer): hint "This unit shouldn't appear in 1.99 mission. Please report bug to the author.", exit
? _vt in structsOccupiableMortar: 3 setRadioMsg "Switch 080m/s Magazine", 4 setRadioMsg "Switch 120m/s Magazine", 5 setRadioMsg "Switch 180m/s Magazine", 10 setRadioMsg "Refresh Radio List", exit

; General Structures are allowed to reset the Vector Up.
10 setRadioMsg "Refresh Radio List"; exit


#Abnormal

; Parachute. In crCTI when player in Parachute the "Options" action invalid. No modifying there but add "Options" in radio.
? "ParachuteBase" countType [_vehicle] > 0: 1 setRadioMsg "Options", 10 setRadioMsg "Refresh Radio List", exit

10 setRadioMsg "Refresh Radio List"; exit

; EOF